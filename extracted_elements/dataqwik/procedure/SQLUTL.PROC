//DO NOT MODIFY  SQL Utilities|SQLUTL|||||||1
SQLUTL
	/*
	ORIG: 6915 - 09/19/2002
	DESC: SQL Utilities
 
	---- Comments --------------------------------------------------------
	This procedure contains SQL related utilities.
 
	---- Revision History ------------------------------------------------
	07/10/07 - Pete Chenard - 28171
		Added new function BSE for M routines that need to use 
		byte string extract functions.
		
	07/25/06 - Pete Chenard - 20048
		Corrected parameter type in BSL section and added () to the
		end of the .length method in that section.
	*/

	quit

public ROWS(ByteString str)
	/*
	This function returns the number of rows contained in
	the length-value string 'str'.
	
	ARGUMENTS:
	     .str	Length-value string

	RETURNS:
	     .$$	Number of rows
	*/

	type ByteString del,row,rows
	type Number i,size

	set del=255.byte()
	set size=0

	for i=1:1:str.length(del)-1 do {
		set row=str.piece(del,i)
		set size=size+(row.ascii(8)*256)
		set size=size+(row.ascii(9))+2
	}

	set rows=(1022000-str.length())\(size+2)
	if rows<1 set rows=1

	quit rows


public SQLDTA(ByteString str,
              ByteString sqlcur,
              ByteString sqldta)
	/*
	This function wraps string 'sqlcur' with a 2 byte header
	and appends it to string 'str'.

	ARGUMENTS:
	     .str	String
	     .sqlcur	Cursor
	     .sqldta	Data

	RETURNS:
	     .$$	Length-value string
	*/

	type ByteString v
	type Number len
	type String del

	set del=255.byte()

	set v=sqlcur_del_str
	set len=v.length() 
	set sqldta=(len\256).byte()_(len#256).byte()_v_sqldta

	quit sqldta


LVU(ByteString str, Number ptr)
	/*
	This function unwraps and removes the header of
	length-value string 'str' at the specified
	location within the string (ptr).

	ARGUMENTS:
	     .str	Length-value string
	     .ptr	String pointer

	RETURNS:
	     .$$	Unwrapped string
	*/

	type ByteString z
	type Number len

	if 'ptr.exists() set ptr=0
	set len=str.ascii(ptr+1)*256
	set len=len+str.ascii(ptr+2)

	set ptr=ptr+len
	set z=str.extract(ptr-len+3,ptr)
	if ptr=str.length()!(len<0) set ptr=0

	quit z


LVW(ByteString str)	
	/*
	This function wraps string 'str' with a 2 byte header.

	ARGUMENTS:
	     .str	String

	RETURNS:
	     .$$	Length-value string
	*/

	type Number len

	set len=str.length()+2
	if len<256 quit 0.byte()_len.byte()_str
	quit (len\256).byte()_(len#256).byte()_str

BSL(String str)
	/*
	This function provides the byte string length for a string. It is
	a wrapper for .toByteString().length() for use by existing M routines
	until they are converted to PSL, at which point this can be removed.
	ARGUMENTS:
	.str String
	RETURNS:
	.$$ Length of string in bytes /TYP=N
	*/
	quit str.toByteString().length()


public BSASCII(ByteString INSTRING, // Input string
             Number POS)            // Position

      /*
      Return ByteString.ascii in support of code still in M that needs to
      be Unicode compliant.

      Once all code using this is converted to PSL, this section can be
      eliminated.
      */      

      quit INSTRING.ascii(POS)


public BYTECHAR(Number INNUM,       // Input number
		Number INNUM2,
		Number INNUM3,
		Number INNUM4,
		Number INNUM5,
		Number INNUM6,
		Number INNUM7,
		Number INNUM8,
		Number INNUM9,
		Number INNUM10,
		Number INNUM11,
		Number INNUM12)

      /*
      Return Number.byte in support of code still in M that needs to
      be Unicode compliant.

      Once all code using this is converted to PSL, this section can be
      eliminated.

      */
      type ByteString str
      
	set str = INNUM.byte()
	if INNUM2.exists() set str=str_INNUM2.byte()
	if INNUM3.exists() set str=str_INNUM3.byte()
	if INNUM4.exists() set str=str_INNUM4.byte()
	if INNUM5.exists() set str=str_INNUM5.byte()
	if INNUM6.exists() set str=str_INNUM6.byte()
	if INNUM7.exists() set str=str_INNUM7.byte()
	if INNUM8.exists() set str=str_INNUM8.byte()
	if INNUM9.exists() set str=str_INNUM9.byte()
	if INNUM10.exists() set str=str_INNUM10.byte()
	if INNUM11.exists() set str=str_INNUM11.byte()
        if INNUM12.exists() set str=str_INNUM12.byte()
      quit str

BSE(String str, Number start, Number end)
	/*
	This function provides the byte string extract for a string. It is
	a wrapper for .toByteString().extract() for use by existing M routines
	until they are converted to PSL, at which point this can be removed.

	ARGUMENTS:
	.str   - String
	.start - Start of Extract
	.end   - End of Extract

	RETURNS:
	.$$ Extract of string in bytes /TYP=T
	*/
	quit str.toByteString().extract(start,end)

BSP(String str, String del, Number pos, Number endpos)
	/*
	This function provides the byte string $piece for a string. It is
	a wrapper for .toByteString().piece() for use by existing M routines
	until they are converted to PSL, at which point this can be removed.

	ARGUMENTS:
	.str   -  String
	.del   -  Delimiter
	.pos   -  Position
	.endpos - Ending Position

	RETURNS:
	.$$ $Piece of string in bytes /TYP=T
	*/
	if 'endpos.get().isNull() quit str.toByteString().piece(del,pos,endpos)
	quit str.toByteString().piece(del,pos)
