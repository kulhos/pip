//DO NOT MODIFY  Oracle/DB2 Database Utilities|DBSDBASE|||||||1
DBSDBASE
	
	// *******************************************************************
	// * IMPORTANT NOTE:                                                 *
	// * According to the rules that apply to PSL compiler upgrades,     *
	// * the generated M routine associated with this procedure must be  *
	// * checked into StarTeam and released with the procedure whenever  *
	// * changes are made to this procedure.                             *
	// *                                                                 *
	// * The mrtns version will be used during upgrades and will then be *
	// * removed from the mrtns directory.  Therefore, other than in a   *
	// * development environment, or during an upgrade, an mrtns version *
	// * of this routine should not exist.                               *
	// *                                                                 *
	// * Keep these comments as single line to ensure they exist in the  *
	// * generated M code.                                               *
	// *******************************************************************
	 
	/*
	Oracle/DB2 Database Utilities
	
	Private labels are available for DATA-QWIK, SQL, and PSL only.

	---- Revision History -------------------------------------------------
	09/07/07 - RussellDS - CR27652
		   Modified handling of PROCLIST in REGENSP to remove use of
		   Error class since that was causing problems with attempt
		   to set to null.
	
	01/29/07 - GIRIDHARAN - CR: 24677
		   Modified section BUILD to quit on a M database.
		   
	08/28/06 - RussellDS - CRs: 22719 / 20613
		   Rewrote VOBJ section to improve efficiency.
	
	06/08/06 - RussellDS - CR21672
		   Modified BUILD section to display errors that occur.  Was
		   referencing undefined vRm previously.  Now displays any
		   errors from returned error array.
	
	03/08/06 - GIRIDHARAN - CR19999
		   Modified section BUILD to pass in the rdb table name to 
		   section REGENSP
	
	03/01/06 - Giridharanb - CR19860
		   Added a function BUILD to support mass regeneration of oracle
		   stored procedures..
		   
	02/23/06 - Frans S.C. Witte - CR19761
		   Funcion $$value(): for datatype 'L' an empty string will
		   unconditionally translate into zero.

	02/10/06 - Pete Chenard - CR 19161
		   Added LIST function, which returns the list of columns
		   for a specified table in the order in which they exist in 
		   the relational database.
		   
	02/02/06 - RussellDS - CR19376
		   Added CREATESP and REGENSP sections.  Modified RUNSP section
		   to use CREATESP.
	
	01/18/06 - RussellDS - CR19046
		   Modified VOBJ section to correctly get node information for
		   blob/memo fields - replace node with position on construction
		   of vobj reference.
	
	10/25/05 - RussellDS - CR18072
		   Converted to PSL.
		   
		   Removed old revision history.
		   
	09/07/05 - GIRIDHARANB - CR16791
		   Modified section VOBJ to use the -151 level. Also modified call
		   call to EXECCP^%DBAPI in section RUNSP to enable error message 
		   to returned correctly (per Frans S.C. Witte)
	
	06/14/05 - GIRIDHARANB - CR16287
		   Modified section VOBJ to use a secondary update statment to 
		   file large object columns to the database.
		   Code review change to add comment on varaible table.
	
	06/03/05 - GIRIDHARANB - CR16152
		   Modified section wide to use tab as delimiter.See associated
		   changes in DBMAP.
	 
	04/28/05 - RussellDS - CR14908
		   Added code in SQL section to include protection logic.
	
		   Corrected logic for binding host variables in SQL section
		   to allow host variables on the left hand side of an
		   expression.
	
		   Corrected logic in WHR section to deal with NOT and with
		   parentheses in expressions.
	
	03/11/05 - GIRIDHARANB - CR14780
		   Modified section value to support "'" in the column value and
		   support the clock time data type "C".
		   Modified section BIND to support binding null values to columns
		   by treating them as zeros.
		   Modified section SQL to remove the duplicate column check
		   in the sql select statement.
		   Modified section ORDERBY to accomodate table name "." column
		   name syntax in the orderby clause.
	
	02/11/05 - GIRIDHARANB - CR14407
		   Modified section VOBJ to use the delimiter from the file
		   definition. 
		   Modified section SPLIT to correct error with duplicate columns
		   in the select list.
		   Modified section RUNSP to change the DBTBL18 reference to point
		   to DBTBLSP.
	
	01/19/05 - GIRIDHARANB - CR13233
		   Replaced references to ^SQLMAP with SQLMAP^DBMAP.
		   Modified section SPLIT to correct error with sql string.
	-----------------------------------------------------------------------
	*/

	quit		// Do not call from top


private VOBJ(Record recobj,	// PSL Object
	     String del)	// Table delimiter character
	     
	/*
	Determine appropriate sql update and insert statements, along with
	bind variables, based on vobj(obj), and update the database.
	*/
	
	type public String %DB, vobj(,)
	
	type Boolean isBM
	type Number bindnum, corder, obj, plusind, position, procmode, vER
	type String bmcol, col, inscols, insvals, node, rdbtbl
	type String sql, type, value, vlist, vRM, WHERE, X
	
	// Remove this when we eventually can deal with the proper passing
	// of an object and use of the object pointer
	#ACCEPT Date=09/01/2006; Pgm=RussellDSR; CR=22719; Group=DEPRECATED
	set obj = recobj.getPointer()
	
	set isBM = 0
	set value = ""
	set bindnum = 1
	set procmode = vobj(obj, -2)

	// No -151 node if no keys, e.g., CUVAR, or if in insert mode
	set WHERE = vobj(obj,-151).get()
	
	/* If insert, do main table(s) first, then blobs/memos
	   If updates, do blobs/memos first, since may be key change and
	   need to make sure blob/memo is updated first.  Control with
	   corder (collating order).
	*/
	if (procmode = 0) set corder = 1
	else  set corder = -1
	
	set (col, node) = ""
	for  set node = vobj(obj, -150, node).order(corder) quit:node.isNull()  do {
		
		set (inscols, insvals, sql, vlist) = ""
		
		// Is this a blob/memo node?
		if node.isLike("%,%") set isBM = 1
		else  set isBM = 0
		
		for  set col = vobj(obj, -150, node, col).order() quit:col.isNull()  do {
		
			// X = position | plusind | rdbtbl
			set X = vobj(obj, -150, node, col)
		
			set position = X.piece("|", 1)
			set plusind = X.piece("|", 2)
			set rdbtbl = X.piece("|", 3)
		
			if position < 0 set value = vobj(obj,position)		// Key
			else  if (node = "0*") set value = vobj(obj).piece(del, position)
			else  if 'isBM set value = vobj(obj, node).piece(del, position)
			else  set value = vobj(obj, position, 1)
			
			/* plus indicator:  0 = never plus
					    1 = plus if not null
					    2 = always plus
			*/
			if (plusind = 2) set value = +value
			else  if (plusind = 1), 'value.isNull() set value = +value
			
			// Blob/memo - NOTE: only 1 blob/memo per node
			if isBM set bmcol = col
			
			// Update mode - non-blob/memo
			else  if (procmode > 0) do {
				
				set sql = sql_","_col_"=:VB"_bindnum
				set bindnum = bindnum + 1
				set vlist = vlist_value_del
			}
			// Insert mode
			else  do {
				
				set inscols = inscols_","_col
				set insvals = insvals_",:VB"_bindnum
				set bindnum = bindnum + 1
				set vlist = vlist_value_del
			}
		}
		
		// Do the update		
		if isBM do {
		
			type String BMWHERE = WHERE
			
			// Strip leading WHERE from where clause
			if BMWHERE.beginsWith(" WHERE ") set BMWHERE = BMWHERE.extract(8, BMWHERE.length())
		
			// On insert, won't have a where clause, so need to build it.  Don't do this
			// earlier, since most of the time we don't need it
			if BMWHERE.isNull() do {
			
				type String col, node
			
				set node = ""
				for  set node = vobj(obj, -150, node).order() quit:node.isNull()  do {
					
					set col = ""
					for  set col = vobj(obj, -150, node, col).order() quit:col.isNull()  do {
					
						set X = vobj(obj, -150, node, col)
		
						set position = X.piece("|", 1)
					
						// If it's a key
						if (position < 0) set BMWHERE = BMWHERE_col_"='"_vobj(obj, position)_"' AND "
					}
				}
				
				set BMWHERE = BMWHERE.extract(1, BMWHERE.length() - 5)
			}
		
			set vER = $$LOBUPDT^%DBAPI(0, rdbtbl, bmcol, BMWHERE, value, del, vlist, .vRM)
		}
		else  do {
			
			if (procmode > 0) set sql = "UPDATE "_rdbtbl_" SET "_sql.extract(2, sql.length())_" "_WHERE
			else  set sql = "INSERT INTO "_rdbtbl_" ("_inscols.extract(2, inscols.length())_") VALUES ("_insvals.extract(2, insvals.length())_")"
				
			set vER = $$EXECUTE^%DBAPI(0, sql, del, vlist, .vRM)
		}
			  
		if (vER < 0) throw Class.new("Error", "%PSL-E-RDBSAVEFAIL,"_vRM.get().translate($C(10, 44), $C(32, 126)))
	}
	
	kill vobj(obj, -150)
	
	quit


private BIND(String expr,	// Expression
	     String array())	// Code array		/MECH=REFARR:RW
	     
	/*
	Construct code to bind variables
	*/
	
	type public String var1()	// Data types
	
	type Number I
	type String code, var
	
	quit:'expr.isLike("%:%")	// No variables to bind
	
	set code = "S vList="
	
	for I = 2:1:expr.length(":") do {
		
		set var = expr.piece(":", I).piece(" ", 1)

		if 'var.isNull() do {
		
			if 'var1(I-1).get().isNull(), ("N$L" [ var1(I-1)) set code = code_"+"_var_"_$C(9)_"
			else  set code = code_"""'""_"_var_"_""'""_$C(9)_"
		}
	}
	
	set code = code.extract(1, code.length() - 7)	// Strip trailing _$C(9)_
	
	set array = array.get() + 1
	set array(array) = code
	
	quit
	

private RUNSP(String expr,	// SQL expression
	      String colnam,
	      String tabnam,
	      String cnd,
	      String hostval,
	      String RM)
	      
	/*
	Returns the SP name if it exists, otherwise creates stored
	procedure on the Oracle database and then returns the name.
	
	Throws an error if error encountered.  ER retained as public
	variable for backward compatibility.
	
	NOTE:  This section will go away once the current caller is
	       modified to call CREATESP directly.
	*/
	
	type String INPUT()
	
	set INPUT("SQL") = expr
	set INPUT("WHERE") = cnd
	set INPUT("HOSTVARS") = hostval
	
	quit $$CREATESP(tabnam, "SelectAll", .INPUT(), 0)
	

private CREATESP(String RTBL,		// Table name [*]
		 String METHOD,		// Name of method procedure created for [*]
		 String INPUT(),	// Input to procedure creation [*]
		 Boolean REGEN)		// Force regeneration [*]
		 
	/*
	Create a stored procedure, return the name.
	
	ARGUMENT NOTES:
	
		. RTBL		Either the RDB table name or $DB for procedures
				that are database wide
				
		. METHOD	Identifies the method for which the procedure is
				create since each method has its own naming
				convention and approach to generating the supporting
				procedure.
				
				The allowed method names are documented below.
				
		. INPUT		The contents of this array vary based on the method
				and are documented for each method.  INPUT is saved
				in DBSPID to be used to determine if a request for
				a procedure duplicates one that already exists.
				
		. REGEN		Allows forced regeneration of a stored procedure.
				
	RETURNS:
		. $$		Procedure name
		
		. Error		Throws an error if an error is encountered
		
	EXAMPLE:
	
		set spname = $$CREATESP^DBSDBASE("ACN", "SelectAll", .INPUT(), 0)
		
	Methods:
	
		. SelectAll	SELECT * from RTBL for a single row
		
				  INPUT("SQL") = select statement
				  INPUT("WHERE") = where clause
				  INPUT("HOSTVARS") = host variable list
				
				Name = T_tablename$SEL_ALL
				  
	Notes:	Additional methods are planned for the future.
		If we begin using packages, the names may change.
	*/
	
	type Boolean ER
	type Number seq
	type String N, procdata, RM, spname
	
	// Protect from incorrect scoping in %DBAPI - remove once %DBAPI fixed
	type String del = ""
	
	set (N, procdata) = ""
	for  set N = INPUT(N).order() quit:N.isNull()  do {
		
		set procdata = procdata_N_"="_INPUT(N)_13.char()_10.char()
	}
	
	set spname = ""
	
	// See if procedure already generated and, if so, just return name
	if 'REGEN do {
	
		type ResultSet rs = Db.select("SPNAME", "DBSPID", "RTBL=:RTBL AND METHOD=:METHOD AND PROCDATA=:procdata")
		
		if rs.next() set spname = rs.getCol("SPNAME")
	}

	quit:'spname.isNull() spname
	
	if (METHOD = "SelectAll") do {
		
		set spname = "T_"_RTBL_"$SEL_ALL"
		set seq = 0				// Only one of these
	
		set ER = $$EXECCP^%DBAPI(0, spname, "*", RTBL, INPUT("WHERE"), INPUT("HOSTVARS"), .RM)
	}
	
	else  do {
		
		set ER = 1
		set RM = "Invalid method name"
	}
		
	if 'ER do {
		
		type RecordDBSPID dbspid = Db.getRecord("DBSPID", "RTBL=:RTBL,METHOD=:METHOD,SEQ=:seq", 1)
		
		// Note that this may replace an existing, different procedure name.
		// This will only occur if we change our naming conventions.
		set dbspid.spname = spname
		set dbspid.procdata = procdata
		set dbspid.lastdate = %CurrentDate
		set dbspid.lasttime = %CurrentTime
			
		do dbspid.save()
	}
	
	if ER throw Class.new("Error", "%DQ-E-CREATESP,"_RM.get().replace(",","~"))
	
	quit spname
	
	
private REGENSP(String RTBL,		// Relational table name [*]
		String PROCLIST(,))	// Procedures regenerated [*]	/MECH=REFARR:W
		 
	/*
	Regenerate all the stored procedures for a given table, return the names.
	
	ARGUMENT NOTES:
	
		. RTBL		Either the RDB table name or $DB for procedures
				that are database wide
				
		. PROCLIST	Array returned containing procedure name, method,
				and description of any errors that occur
				
				  PROCLIST(spname, method) = error message or
				  			     null if no error
				
	RETURNS:
		. $$		0 if no errors
				number of errors otherwise

	EXAMPLE:
		set X = $$REGENSP("ACN", .PROCLIST(,))
	*/
	
	type Boolean REGENER
	type String CRLF
	
	set REGENER = 0
	set CRLF = 13.char()_10.char()
	
	type DbSet ds = Db.selectDbSet("DBSPID", "RTBL=:RTBL")
	
	while ds.next() do {
		
		type Number I
		type String input(), procdata, spname, X
		
		type RecordDBSPID dbspid = ds.getRecord("DBSPID")
		
		set procdata = dbspid.procdata
		
		for I = 1:1:procdata.length(CRLF) - 1 do {
			
			set X = procdata.piece(CRLF, I)
			set input(X.piece("=", 1)) = X.piece("=", 2, X.length())
		}
		
		catch regenerr {
			
			set REGENER = REGENER + 1
			set PROCLIST(dbspid.spname, dbspid.method) = regenerr.description
		}
		
		set spname = $$CREATESP(RTBL, dbspid.method, .input(), 1)
		
		set PROCLIST(spname, dbspid.method) = ""
	}
	
	quit REGENER
	

private wide(String dqtable)	// DATA-QWIK table name

	/*
	Wide table check

	Checks if the specified table is split into multiple smaller tables.

	RETURNS:
		. $$	0 - not a wide table		/TYP=L
			1 - is a wide table
	*/
	
	type public String %DB

	type RecordDBMAPT dbmapt = Db.getRecord("DBMAPT", "DB=:%DB,TBL=:dqtable", 1)
	
	quit +dbmapt.issplit
	
	
Public LIST(table)     // returns ordered list of columns for a specified table.
 
	type String list=""
	type String col
	type ResultSet rs=Db.select("COL,POS","DBMAP","TBL=:table","POS ASC")
	while rs.next() do {
		set col=rs.getCol(1)
		quit:col.isLiteral()
		set list=list_","_rs.getCol(1)
	}
	set list=list.extract(2,9999)
	quit list

public BUILD    // Build Stored procedures for all tables in the DB
 
        type Number CNT,i,vEr
	type String dbid,rfid,SPLIST(,)
	
	set dbid=$$TRNLNM^%ZFUNC("SCAU_DB")		
 
        do Db.delete("TMPDQ","PID=:%ProcessID")
 
        set CNT = $$LIST^DBSGETID("DBTBL1")            // Select table names 
        quit:(+CNT=0)
        
        if dbid="GTM" write $$MSG^%TRMVT("",,1) quit
 
        type DbSet ds = Db.selectDbSet("TMPDQ","PID=:%ProcessID","ELEMENT ASC")
        while ds.next() do {
                type RecordTMPDQ tmpdq = ds.getRecord("TMPDQ")
		set rfid=tmpdq.element
                do MAP^DBMAP(dbid,.rfid)
                if rfid.length(",")>1 for i=1:1:rfid.length(",") do {
                        set vEr=$$REGENSP(rfid.piece(",",i),.SPLIST(,))
                }
                else  set vEr=$$REGENSP(rfid,.SPLIST(,))
                
                if vEr do {
                	
                	type String ERDESC, N, SP
                	
                	// Errors: ~p1
                	write !!, $$^MSG(6819)
                	
                	set (N, SP) = ""
                	for  set SP = SPLIST(SP).order() quit:SP.isNull()  do {
                		
                		for  set N = SPLIST(SP, N).order() quit:N.isNull()  do {
                			
                			set ERDESC = SPLIST(SP, N)
                			
                			if 'ERDESC.isNull() write ?10, SP, " - ", N, ":  ", ERDESC, !
                		}
                	}
                }
        }
 
        do Db.delete("TMPDQ","PID=:%ProcessID")
 
        // Press any key message and pause
        write $$MSG^%TRMVT("",,1)
 
        quit
