USUB
	/* 
 	 PROCEDURE ID:	USUB
 	     Original:	FSANCHEZ -  5 MAY 1993
		 Date:	9/26/2002 - 10:04:53 AM
 	         Desc:	Sub Field Get/Put Utility

	---------- Revision History ------------------------------------------

	3/7/2002 - Vitaliy Antonov - 43583
		Converted To PSL.

	------------------------------------------------------------------------
	*/
	quit

public	GET(V,sft,sfd1,sfd2,sfp)	// Get a value (substring) from a string

	if $G(sft)="",$G(sfd1)="",$G(sfd2)="" quit $E(V,sfp)
	if '$G(sfp) set sfp=1

	if sft="" quit $P(V,sfd1,sfp)

	if $G(sfd2)="" set sfd2=sfd1

	quit $P($P($P(sfd1_V,(sfd1_sft_sfd2),2),sfd1,1),sfd2,sfp)


public	PUT(V,X,sft,sfd1,sfd2,sfp)	// Poke a value X into a string V

	new field

	if '$G(sfp) set sfp=1

	if sft="" set $P(V,sfd1,sfp)=X quit $$STRIP(V,sfd1)

	if $G(sfd2)="" set sfd2=sfd1
	if V="" set $P(V,sfd2,sfp)=X quit sft_sfd2_V

	set field=$P($P(sfd1_V,(sfd1_sft_sfd2),2),sfd1,1)
	if field'="" do {
		new z
		set z=sfd1_sft_sfd2_field
		set V=$P(sfd1_V,z,1)_$P(sfd1_V,z,2)
		if $E(V)=sfd1 set V=$E(V,2,$L(V))
		}
	set $P(field,sfd2,sfp)=X

	if V="" quit sft_sfd2_field

	quit V_sfd1_sft_sfd2_field


STRIP(V,D)	// Strip trailing D's from V

	for  quit:$E(V,$L(V))'=D  set V=$E(V,1,$L(V)-1)

	quit V


public	PARSE(expr,value,mode)	// Change $$GET^USUB format into SET $P format

	new cnt,del,exp,pos,z,zexp

	set cnt=$L(expr,",")
	set pos=$P(expr,",",cnt)+0
	set del=$P(expr,",",cnt-2)
	set exp=$P(expr,",",1,cnt-4)
	set exp=$P(exp,"(",2,99)
	set z=$P(exp,",",1)
	set zexp=$P(z,"(",1)_"($G("_$P(z,"(",2,99)_")"_","_$P(exp,",",2,99)

	// I18N=OFF
	// create
	quit " S v="_zexp_",$P(v,"_del_","_pos_")="_value_","_exp_"=v"
	// I18N=ON
 #OPTION ResultClass ON
public String vSIG()	quit "59495^75670^Lik Kwan^1604"	// Signature - LTD^TIME^USER^SIZE
