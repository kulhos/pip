SCATIM	//

  /*
ORIG: chhabris - 08/02/2005
DESC: CONVERTS EXTERNAL TIME TO SECONDS

---- Comments --------------------------------------------------------
	 Convert External Time to Seconds
	
	 Note:  Modified version of Intersystems MUMPS utility
	
	 KEYWORDS: Date and Time
	
	 INPUTS:
		. %TS 	Time in string format		/TYP=T/REQ
			For example 1130   1:00P   13:00  2:00A  
			T or C (Current Time)
	
	 RETURNS:
		. %TN 	Time in seconds since 		/TYP=N
			Midnight
	
	 RELATED:
		. D ^SCATIM1	Returns formatted time from time in seconds
	
	 EXAMPLE:
		S %TS="1:00P"
		D ^SCATIM
		W !,%TN
	
---- Revision History ------------------------------------------------

	08/02/2005 - CHHABRIS/ARPAVC - CR16677
		     Converted to PSL.
		     Removed revision history prior to 2004.

----------------------------------------------------------------------	


 */
 
 	do START
 	quit
 	
START	//
	type public Boolean ER
	type public String RM,%TN,%TS

	// Entry exceeds 24 hours
	if %TS?1N.N set:%TS>86400 ER=1,RM=$$^MSG(966) set %TN=%TS quit

	if %TS="T" ! (%TS="C") set %TN=%CurrentTime quit
	set %TS=%TS.translate(" ")
	if %TS="12:00A" ! (%TS="12:00AM") set %TN=1 quit

	// convert if format is military time  00:00:00 to 24:59:59
	if (%TS?1N.N1":"1N.N!(%TS?1N.N1":"1N.N1":".N)),%TS<24 set %TN=(((%TS.piece(":",1)*60)+%TS.piece(":",2))*60)+%TS.piece(":",3) quit
	do AMPM
	quit


AMPM	//  Convert if standard format (time with AM or PM)
	type public String %TS
	type Number %M,%S,%X,%Y,%Z

	set %M=0,%S=0
	if %TS?4N do CNVHHMM quit
	set %X=%TS.piece(":",1),%Y=%TS.piece(":",2),%Z=%TS.piece(":",3,99)

	if %Z]]"" do SEC quit
	if %Y]]"" do MIN quit
	do HRS
	quit

CNVHHMM	//  Hours and Seconds in HHMM (no semicolon)
	type public String %TN,%TS
	type public Number %H,%M
	
	set %H=%TS.extract(1,2),%M=%TS.extract(3,4)
	if %H > 23!(%M > 60) set %TN=-1 quit

	set %TN=(%H*3600)+(%M*60)
	quit

SEC	//  Convert Format in Hours, Minutes, Seconds  (12:00:00AM to 11:59:59PM)
	type public Number %H,%M,%S,%X,%Y,%Z
	type public String %TN

	if %X?1N!(%X?2N),%X<13,%X>0 set %H=%X+0
	else  set %TN=-1 quit
	if %H>12 ! (%H<1) set %TN=-1 quit
	if %Y?2N,%Y<60 set %M=%Y+0
	else  set %TN=-1 quit
	set %S=%Z+0 if %S<0!(%S>59) set %TN=-1 quit
	if %Z?2N1U.U set %Z=%Z.extract(3,99) do CALC quit
	if %Z?2N1" "1U.U set %Z=%Z.extract(4,99) do CALC quit
	set %TN=-1
	quit

MIN	//  Convert Format in Hours, Minutes (12:00AM to 11:59PM)
	type public Number %H,%M,%X,%Y,%Z
	type public String %TN
	
	if %X?1N!(%X?2N),%X<13,%X>0 set %H=%X+0
	else  set %TN=-1 quit
	if %H>12 ! (%H<1) set %TN=-1 quit
	set %M=%Y+0 if %M<0!(%M>59) set %TN=-1 quit
	if %Y?2N1U.U set %Z=%Y.extract(3,99) do CALC quit
	if %Y?2N1" "1U.U set %Z=%Y.extract(4,99) do CALC quit
	set %TN=-1
	quit

HRS	//  Convert Hours  (12AM to 11PM)
	type public Number %H,%X,%Z
	type public String %TN
	
	if %X?1N.N1" "1U.U set %H=%X+0,%Z=%X.piece(" ",2)
	else  if %X?1N1U.U set %H=%X+0,%Z=%X.extract(2,99)
	else  if %X?2N1U.U set %H=%X+0,%Z=%X.extract(3,99)
	else  set %TN=-1 quit
	if %H>12 ! (%H<1) set %TN=-1 quit
	do CALC
	quit

CALC	// compute final time

	type public Number %H,%M,%S,%Z
	type public String %TN

	if %Z="P" ! (%Z="PM") set:%H<12 %H=%H+12
	else  if %Z="N" ! (%Z="NOON"),%H=12,%M=0,%S=0 set %TN=43200 quit
	else  if %Z="M"!(%Z?1"MID".E),%H=12,%M=0,%S=0 set %TN=86400 quit
	else  if %Z="A" ! (%Z="AM") set:%H=12 %H=0
	else  set %TN=-1 quit

	set %TN=(%H*3600)+(%M*60)+%S

	quit


	//-----------------------------------------------------------------------
NS	/* Public Convert time from 'seconds since midnight' to Formatted time
	 	   Input  %TN (NNNNN)
		  Output  %TS HH:MM PM/AM
	
	 KEYWORDS: Date and Time
	
	 INPUTS:
		. %TN 	Time in seconds since 		/TYP=N
			Midnight
	 RETURNS:
		. %TS 	Time in string format		/TYP=T/REQ
			For example 1130   1:00P   13:00  2:00A  
			T or C (Current Time)
	
	 RELATED:
		. D ^SCATIM1	Returns formatted time from time in seconds
	
	 EXAMPLE:
		S %TN="1:00P"
		D NS^SCATIM
		W !,%TS
		
	-----------------------------------------------------------------------
	*/

	type public Boolean ER
	type public String RM,%TN,%TS
	type Number %H,%M,%S

	if %TN="" set ER=1 quit

	// Entry exceeds 24 hours
	if %TN > 86400 set ER=1,RM=$$^MSG(966) quit

	set %H = (%TN\3600)+100,%M=((%TN#3600)\60)+100,%S=(%TN#60)+100
	if %H > 112 set %H=%H-12
	if %H = 100 set %H="112"
	set %TS = %H.extract(2,3)_":"_%M.extract(2,3)
	if %TN = 0 ! (%TN=86400) set %TS=%TS_" M" quit
	if %TN < 43200 set %TS=%TS_"AM" quit
	if %TN = 43200 set %TS=%TS_" N" quit
	set %TS=%TS_"PM"
	quit
 #OPTION ResultClass ON
public String vSIG()	quit "60178^29149^Sanjay Chhrabria^4351"	// Signature - LTD^TIME^USER^SIZE
