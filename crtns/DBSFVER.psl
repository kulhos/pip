DBSFVER	
		 
	/*
	Table Relationship Verification
	
	NOTE:  Call at top is retained for backward compatibility.  Once
	       all callers have been eliminated, remove it.
	       
	       New call for things converted to PSL is VALIDATE.

	---- Revision History -------------------------------------------------
	05/01/06 - RussellDS - CR20967
		   Converted to PSL.

	-----------------------------------------------------------------------
	*/
	
	/*
	INPUT if called at top:
	
		. FILES		Access tables		/TYP=T/REQ
				(Primary, secondary, ...)
				
	OUTPUT:
	
		. ER/RM		Error flag/message
	*/
	
	type public Boolean ER
	type public String FILES, RM
	
	set ER = $$VALIDATE(FILES, .RM)
	
	quit
	
	
public VALIDATE(String TABLES,		// Access tables [*]
		String RM)		// Error message	/MECH=REFNAM:W
		
	/*
	Validate relationship between tables
	
	ARGUMENT NOTES:
	
		. TABLES	List of table to validate.  Primary table is
				listed first.
				
	RETURNS:

		. $$		Error indicator 		/TYP=L
				(0 = no error, 1 = error)
				If 1, RM has error message
	*/
	
	type public String I()			// Screen look-up table
	
	type Boolean ER
	type Number J
	type String LOOP(,), PRMRYTBL
	
	set ER = 0
	set (PRMRYTBL, RM) = ""
	
	if TABLES.isNull() quit 0
	
	if TABLES.isLike("%,%") set I(3) = ""
	
	set PRMRYTBL = TABLES.piece(",", 1)
	
	for J = 1:1:TABLES.length(",") do { quit:ER
		
		type Number K
		type String keys, TBL
		
		set TBL = TABLES.piece(",", J)
		
		if TBL.isNull() do { quit
			
			set ER = 1
			// Invalid syntax - ~p1
			set RM = $$^MSG(1477, TABLES)
		}
		
		if 'Db.isSchemaTable(TBL) do { quit
			
			set ER = 1
			// Invalid file - ~p1
			set RM = $$^MSG(1334, TBL)
		}
	}
	
	if ER quit 1
	
	if (TABLES.length(",") = 1) quit 0		// Single table
	
	set ER = $$VALIDATE^DBSREL(TABLES, .LOOP(,), .RM) if ER do {
		
		// Invalid files relationship - ~p1
		set RM = $$^MSG(1346, TABLES)
	}
	
	else  if (LOOP(-2, 1).get() '= PRMRYTBL) do {
	
		set ER = 1
		// Select ~p1 for primary file ID
		set RM = $$^MSG(2470, LOOP(-2, 1))
	}
	
	quit ER
 #OPTION ResultClass ON
public String vSIG()	quit "60425^2727^Dan Russell^1929"	// Signature - LTD^TIME^USER^SIZE
