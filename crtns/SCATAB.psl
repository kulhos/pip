SCATAB
	// SCA Standard %TAB Definitions
	/*
	  ORIG:  MATTSON - 26 JUN 1990
	
	  ---- Revision History ------------------------------------------------

	  11/21/05 - PUTTASWH - CR17011
	  	     Removed the line set dbtbl1d=""
	  	     Declared the varibles to avoid warnings.   	
	   
	  09/28/01 - MBUIM - 43583
		     Converted to PSL	  

	*/
	
	quit
		

Public IO(DFT)	// Return %TAB("IO")

	/*
	   KEYWORDS: Formatting
	
	   ARGUMENTS:
	       . DFT Default value for 'IO' variable  	/TYP=T/NOREQ
		     Displayed as default when prompting 
		     for device on UTLREAD screen.
	
	   INPUTS:
	       . %IPMODE Mode, Burroughs or Native  	/TYP=T/NOREQ
		     Used to determine IO type and page 
		     lengtH When in 'burroughs poll 
		     select' mode
	
	   RETURNS:
	       . $$ Formatted %TAB("IO") entry with  	/TYP=T
		    table lookup and post processor.
		    Also will return IO=DFT value.
	    Note: Returns null if %IPMODE["NOINT"
	   

	*/
	type String IOTYP
	type Number IOSL
	if $G(%IPMODE)["NOINT" do { quit ""     
		if $G(IO)="" do {
			set %NOOPEN="" 
			set IO=$I 
			set IOTYP="TRM" 
			set IOSL=22
			}  
		}
	if $G(DFT)'="" set IO=DFT
	
	quit ".IO4/TBL=[DEVICE]DEVNAME:NOVAL/XPP=S %EXT=1 do ^SCAIO"    
	

Public DI(DI,DES,LEN,TBL,XPP,XPR,TYP,MIN,MAX,DEC)	// Formatted %TAB entry	

	/*
	   KEYWORDS: Formatting
	
	   ARGUMENTS:
	       . DI Data Item Name   				/TYP=T/NOREQ/COND
		    format = [FID]DINAM to be used in 
		    %TAB definition
	
	       . DES Prompt for UTLREAD  			/TYP=T/NOREQ
		     screen, overrides dictionary description
	
	       . LEN Data entry length  			/TYP=N/NOREQ
		     for UTLREAD screen prompt, overrides 
		     dictionary length value
	
	       . TBL Lookup table name 				/TYP=T/NOREQ
	
		     The table look-up for the UTLREAD screen prompt.
		     This may be any valid lookup table syntax (local
		     variable name, global variable name, file ID,
		     etc.).  Overrides the dictionary table value,
		     if any.
	
	       . XPP Post processor    				/TYP=T/NOREQ
	
		     MUMPS / DATA QWIK code to be executed upon
		     entry of a value on the prompt.  Overrides
		     dictionary value, if any.
	
	       . XPR Pre processor   				/TYP=T/NOREQ
	
		     MUMPS / DATA QWIK code executed before reaching
		     the field for data entry.  Overrides dictionary
		     value, if any.
	
	       .TYP Data type   				/TYP=T/NOREQ
	
		    Any valid data type (ie TN$DCLUF) supported
		    by PROFILE / DATA QWIK.  Overrides dictionary
		    type value.
	
	       . MIN Minimum value   				/TYP=N/NOREQ
	
		     The minimum value allowed for this field.
		     Overrides dictionary value, if any.
	
	       . MAX Maximum value   				/TYP=N/NOREQ
	
		     The maximum value allowed for this field.
		     Overrides dictionary value, if any.
	
	       . DEC Decimal precision  			/TYP=N/NOREQ
	
		     The decimal precision of the value entered.
		     Overrides dictionary value, if any.
	
	   INPUTS:
	   . System  %LIBS - System library name
	
	   RETURNS:
	   . $$ Returns formatted %TAB value used by ^UTLREAD  	/TYP=T
		utility to build DATA QWIK like data entry or
	    	display screens.  This entry will include the
	    	override value specified by the arguments passed
	    	to the utility or the dictionary values.
	
	   Note:   If no DI ([FID]DINAM) value is given then the override
	    parameters are required.
	
	   Returns formatted %TAB for UTLREAD based either on file definition
	   values if DI=[FID]DINAM with overrides as specified, or on input
	   values where DI="".
	
	   If DI is null, uses values passed in to create %TAB.  In this case,
	   prompt must be defined, data type will default to text, and length
	   may default.
	
	   If $g(any other parameter) is null, will use file values.
	   To force no table, TBL="NOTABLE"
	
	   Call by:  S %TAB("XXX")=$$DI^SCATAB("[LN]BRCD")
		or:  S %TAB("XXX")=$$DI^SCATAB("","Prompt",5,,,,"T")
	
	   Returns "" if problems
	

	*/
	
	new DINAM,ER,FID,X
	
	set ER=0
	
	type RecordDBTBL1D dbtbl1d
	
	if DI'="" do { if ER quit ""
		set FID=$P($P(DI,"]",1),"[",2) 
		set DINAM=$P(DI,"]",2)
		set dbtbl1d=Db.getRecord("DBTBL1D","'SYSDEV',FID,DINAM")
		}
	if DI="" set dbtbl1d=Class.new("RecordDBTBL1D")

	if $G(DES)="" set DES=dbtbl1d.des 
	if DES="" quit ""
	if $G(TYP)="" set TYP=dbtbl1d.typ set:TYP="" TYP="T"
	
	if $G(LEN)="" do { if LEN="" quit ""
		set LEN=dbtbl1d.len
		if LEN quit 
		if TYP="$"!(TYP="F")!(TYP="N") set LEN=12 quit 
		if TYP="T"!(TYP="U") set LEN=40 quit 
		if TYP="D" set LEN=10 quit 
		if TYP="C" set LEN=8 quit 
		if TYP="L" set LEN=1 quit 
		}
	
	// Prompt/data type/length
	set X="/DES="""_DES_"""/TYP="_TYP_"/LEN="_LEN  
	
	// Table look-up
	if $G(TBL)="" set TBL=dbtbl1d.tbl
	if TBL'="",TBL'="NOTABLE" set X=X_"/TBL="_TBL
	
	// Post-processor
	if $G(XPP)="" set XPP=dbtbl1d.xpo
	if XPP'="" set X=X_"/XPP="_XPP
	
	// Pre-processor
	if $G(XPR)="" set XPR=dbtbl1d.xpr
	if XPR'="" set X=X_"/XPR="_XPR
	
	// Minimum value
	if $G(MIN)="" set MIN=dbtbl1d.min
	if MIN'="" set X=X_"/MIN="_MIN
	
	// Maximum value
	if $G(MAX)="" set MAX=dbtbl1d.max
	if MAX'="" set X=X_"/MAX="_MAX
	
	// Decimal precision
	if $G(DEC)="" set DEC=dbtbl1d.dec
	if DEC="",TYP="$" set DEC=2
	if DEC'="" set X=X_"/DEC="_DEC
	
	quit X
 #OPTION ResultClass ON
public String vSIG()	quit "60227^22950^Hema Puttaswamy^5053"	// Signature - LTD^TIME^USER^SIZE
