SCADRV7	// Userclass Maintenance
	/*
	       ORIG: Frank Sanchez
	       DESC:  UPDATE USERCLASS

	  ---- Revision History ------------------------------------------------

	  01/10/06 - KELLYP - CR 18937
	  	     Modified FILE section to quit after the Db.delete call when
	  	     %O=3 (delete).  The Db.delete compiles into a "do vDel" 
	  	     statement which can then result in $T changing back to 0
	  	     (which then causes the else statement to be executed once
	  	     control returns to the FILE section).

	  03/28/05 - KELLYP - CR 13666
	  	     Re-introduced this procedure as it was never added to source
	  	     control.  Modified this procedure to conform to current PSL 
	  	     standards and removed VPG/VPG00/VPG01 logic.  Also removed 
	  	     pre-2003 revision history.

	  ---------------------------------------------------------------------
	  */

	quit

NEW	 // Userclass Creation

	do INIT(0)

	quit


UPD 	// Userclass Maintenance

	do INIT(1)

        quit


INQ 	// Userclass Inquiry

	do INIT(2)

        quit


DEL	// Userclass Deletion

	do INIT(3)

        quit


INIT(Number %ProcessMode) // Screen Control

	type Number %OSAVE
	type String UCLS,VFMQ

	set %OSAVE=%ProcessMode

	do QUERY
	
	if "Q"[VFMQ do END quit
	
	type RecordSCAU0 scau0
	
	do SCREEN(.scau0)

	do FILE(.scau0)

        quit


QUERY	// Set up query screen for Userclass selection

	type String %NOPRMT,%READ,%TAB()

	set %TAB("UCLS")=".UCLS4/HLP=[SCAU0]UCLS/XPP=D PPUCLS^SCADRV7"
	if %ProcessMode set %TAB("UCLS")=%TAB("UCLS")_"/TBL=[SCAU0]"
	if %ProcessMode=2 set %TAB("IO")=$$IO^SCATAB($I)

	set %READ="@@%FN,,,UCLS/REQ" 
	if %ProcessMode=2 set %READ=%READ_",IO/REQ"

	set %NOPRMT="N"

	do ^UTLREAD 

	quit


PPUCLS	// UCLS post processor

	type Public Boolean ER
	type Public Number %OSAVE
	type Public String X

	if X.isNull() quit
	
	if %OSAVE=3 do { quit:ER
		
		type DbSet ds=Db.selectDbSet("SCAU")
        	while ds.next() do { quit:ER
                
                	type RecordSCAU scau=ds.getRecord("SCAU")
			
			// Cannot delete - User ID's are tied to Userclass ~p1
			if scau.%ucls=X do Runtime.setErrMSG("SCAU0","8484",X) quit:ER
        		}
		}

	if %OSAVE quit

	// Record already exists
	if Db.isDefined("SCAU0","UCLS=:X") do Runtime.setErrMSG("SCAU0","2327") quit:ER

	quit


SCREEN(RecordSCAU0 scau0) // Userclass screen

	type Public Boolean ER
	type Public String UCLS,VFMQ

      	set scau0=Db.getRecord("SCAU0","UCLS=:UCLS",1)

	if %ProcessMode=2 do OPEN^SCAIO 
	
	if ER set VFMQ="Q" quit

	do DRV^USID(%ProcessMode,"SCAUSRC",.scau0)

	quit


FILE(RecordSCAU0 scau0) // File/Delete the Userclass

	type Public String VFMQ

	if %ProcessMode=2!(VFMQ="Q") do END quit

	if %ProcessMode=3 do { quit
		do Db.delete("SCAU0","UCLS=:scau0.ucls")
		do END
		}

	do scau0.save()

	do END 

	quit


END	// Finish up

	type public String ER,RM,UCLS,VFMQ

	if ER!(%ProcessMode=2) quit  

	set UCLS=UCLS.get()
	set ER="W"
	if VFMQ="Q" do {

		// Userclass ~p1 not created
		if %ProcessMode=0 set RM=$$^MSG(2903,UCLS) quit

		// Userclass ~p1 not modified
		if %ProcessMode=1 set RM=$$^MSG(2905,UCLS) quit

		// Userclass ~p1 not deleted
		if %ProcessMode=3 set RM=$$^MSG(2904,UCLS) quit
		}
	else  do {

		// Userclass ~p1 created
		if %ProcessMode=0 set RM=$$^MSG(4886,UCLS) quit

		// Userclass ~p1 modified
		if %ProcessMode=1 set RM=$$^MSG(4887,UCLS)
		
		// Userclass ~p1 deleted
		if %ProcessMode=3 set RM=$$^MSG(5431,UCLS) quit
		}
	quit
	
 #OPTION ResultClass ON
public String vSIG()	quit "60275^61883^kellytp^3285"	// Signature - LTD^TIME^USER^SIZE
