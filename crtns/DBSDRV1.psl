public DBSDRV1
		 
	/*
	Create functions
	
	Utility to create functions for DQ screens, reports and QWIK reports.

	---- Revision History -------------------------------------------------
	05/01/06 - RussellDS - CR20967
		   Converted to PSL.

	-----------------------------------------------------------------------
	*/
	
	// Call from top to create function to access DQ generated screens
	
	type String OPT(), VFMQ
	
	// Add a new record
	set OPT("A") = $$^MSG("3328")
	// Update an existing record
	set OPT("U") = $$^MSG("3332")
	// Display a record to the terminal
	set OPT("D") = $$^MSG("3329")
	// Print a record to a hard copy device
	set OPT("P") = $$^MSG("3330")
	// Remove an existing record
	set OPT("R") = $$^MSG("3331")
	
	set VFMQ = ""
	
	for  do { quit:(VFMQ = "Q")
		
		type Number %FRAME, OPTION
		type String %READ, %TAB(), ZDES, ZFUN, ZOPT, ZSID

		set %TAB("ZSID") = ".SID1/TBL=[DBTBL2]SID,DESC"
		set %TAB("ZOPT") = ".ZOPT1/TBL=OPT("
		set %TAB("ZFUN") = ".FUN1/TBL=[SCATBL]/XPP=D PP^DBSDRV1"
		set %TAB("ZDES") = ".ZDES1"

		set %FRAME = 2

		set %READ = "@@%FN,,ZSID/REQ,ZOPT/REQ,ZFUN/REQ,ZDES/REQ,"
	
		do ^UTLREAD quit:(VFMQ = "Q")

		set OPTION = "AUDRP".find(ZOPT) - 2
		
		do SET(ZFUN, ZDES, "", "^DBSDEUTL("_OPTION_","_""""_ZSID_""""_")", 0)
	}
	
	quit



public REPORT

	/*
	Create function to access DQ generated reports
	*/
	
	type String VFMQ = ""
	
	for  do { quit:(VFMQ = "Q")
		
		type Number %FRAME
		type String %READ, %TAB(), ZDES, ZFUN, ZRID
		
		set %TAB("ZRID") = ".RID2/TBL=[DBTBL5H]RID,DESC"
		set %TAB("ZFUN") = ".FUN1/TBL=^SCATBL(1,/XPP=D PP^DBSDRV1"
		set %TAB("ZDES") = ".ZDES1"

		set %FRAME = 2

		set %READ = "@@%FN,,ZRID/REQ,ZFUN/REQ,ZDES/REQ,"
	
		do ^UTLREAD quit:(VFMQ = "Q")
		
		do SET(ZFUN, ZDES, "S RID="""_ZRID_"""", "RPT^URID", 1)
	}
	
	quit


public RDIST

	/*
	Create function to access DQ generated reports in report distribution mode
	*/
	
	type String VFMQ = ""
	
	for  do { quit:(VFMQ = "Q")
		
		type Number %FRAME, ZMOD
		type String %READ, %TAB(), ZDES, ZFUN, ZRID
		
		set %TAB("ZRID") = ".RID2/TBL=[DBTBL5H]RID,DESC:QU ""[DBTBL5H]DISTKEY'="""""""""""
		set %TAB("ZFUN") = ".FUN1/TBL=[SCATBL]/XPP=D PP^DBSDRV1"
		set %TAB("ZDES") = ".ZDES1"
		set %TAB("ZMOD") = ".TABFMT"

		set %FRAME = 2

		set %READ = "@@%FN,,ZRID/REQ,ZFUN/REQ,ZDES/REQ,ZMOD/REQ"
	
		do ^UTLREAD quit:(VFMQ = "Q")
		
		do SET(ZFUN, ZDES, "S RID="""_ZRID_""",VMODE="_ZMOD, "EXT^DBSRWDST(RID,VMODE)", 1)
	}
	
	quit
	
	
public QWIKRPT

	/*
	Create function to access DQ generated QWIK reports
	*/

	type String VFMQ = ""
	
	for  do { quit:(VFMQ = "Q")
		
		type Number %FRAME
		type String %READ, %TAB(), ZDES, ZFUN, ZQRID

		set %TAB("ZQRID") = ".ZQRID1/TBL=[DBTBL5Q]QRID,DESC"
		set %TAB("ZFUN") = ".FUN1/TBL=[SCATBL]/XPP=D PP^DBSDRV1"
		set %TAB("ZDES") = ".ZDES1"

		set %FRAME = 2

		set %READ = "@@%FN,,ZQRID/REQ,ZFUN/REQ,ZDES/REQ,"
	
		do ^UTLREAD quit:(VFMQ = "Q")
		
		do SET(ZFUN, ZDES, "S QRID="""_ZQRID_"""", "QRPT^URID", 1)
	}
	
	quit	


PP	// Post processor on ZFUN

	type public Boolean ER
	type public String I(), RM, X

	quit:X.isNull()

	if X.isNumber() do { quit
		
		set ER = 1
		// Invalid format
		set RM = $$^MSG(1350)
	}
	
	if Db.isDefined("SCATBL", "FN=:X") do { quit
		
		set ER = 1
		// Already exists
		set RM = $$^MSG(253)
	}
	
	set I(3) = ""
	
	quit
	
	
SET(String FN,
    String DESC,
    String PRP,
    String PGM,
    Boolean QUEUE)
    
	/*
	Create function
	*/
	
	type RecordSCATBL scatbl = Class.new("RecordSCATBL", "FN=:FN")
	
	set scatbl.desc = DESC
	set scatbl.prp = PRP
	set scatbl.pgm = PGM
	set scatbl.queue = QUEUE
	
	do scatbl.save()
	
	if '%UserClass.get().isNull() do {
	
		type RecordSCATBL3 scatbl3 = Class.new("RecordSCATBL3", "FN=:FN,UCLS=:%UserClass")
		
		set scatbl3.auth = ""
		
		do scatbl3.save()
	}
	
	// Function ~p1 created
	write $$MSG^%TRMVT($$^MSG("1144", FN), "", 1)
	
	quit
 #OPTION ResultClass ON
public String vSIG()	quit "60425^2579^Dan Russell^3676"	// Signature - LTD^TIME^USER^SIZE
