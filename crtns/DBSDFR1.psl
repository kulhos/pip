DBSDFR1	/* Public - Data Dictionary List
	ORIG: Kathie Jalbuena (7070) - 05/18/87
	PROCEDURE ID: DBSDFR1
	DESC: DATA-QWIK Data Dictionary List
	
	---- Comments ---------------------------------------------------------

	------ Revision History -----------------------------------------------
	
	09/19/06 - RussellDS - CR22567
		   Validate where clause entered.

		   Removed old revision history.
	-----------------------------------------------------------------------

	I18N=QUIT Excluded from I18N standards
	*/


	type Public Number ER=0
	type Public String RM

	type Number CNT,DBOPT,DOC,%FRAME,N,OLNTB,TEMP,VRWOPT()
	type String FIL(),H2,IO,PGM,%READ,RID,SORT,%TAB(),VFMQ,vudwhere,WHERE

	do Db.delete("TMPDQ","PID=:%ProcessID")

	set SORT="NOD,POS"

	set H2=" * = All  AB* = From AB to ABz  AB-CD = From AB to CD  'AB = Not AB"

	set %TAB("IO")=$$IO^SCATAB($I)
	set %TAB("DOC")=".LS1"
	set %TAB("SORT")=".SORT1/TBL=@SELDI^DBSFUN(""DBTBL1D"",.X):LIST"
	set %TAB("WHERE")=".WHERE1/XPP=D VALIDWHR^DBSDFR1 I RM'="""" S ER=1"
	set %TAB("FIL")=".FID1/TBL=[DBTBL1]/XPP=D LISTPP^DBSGETID(""DBTBL1"")/LEN=36"
	set %TAB("FIL(0)")=%TAB("FIL")

	set %READ="@@%FN,,IO/REQ,,DOC/NOREQ,SORT/NOREQ,WHERE,,@H2/INC,,FIL(0)/REQ,FIL/REP=10/NOREQ"

	set DBOPT=1,%FRAME=2,OLNTB=20,TEMP=0
	do ^UTLREAD quit:VFMQ="Q"

	set RID="DBSFILLST"
	do ^URID quit:PGM.get()=""
	
	// Add selections to TMPDQ
	set N = ""
	for  set N = FIL(N).order() quit:N.isNull()  set CNT = $$LISTBLD^DBSGETID(FIL(N), "DBTBL1")

	if SORT'="" do {
		type Number I
		type String X

		set X=SORT,SORT=""
		for I=1:1:X.length(",") set SORT=SORT_"DBTBL1D."_X.piece(",",I)_","
		set SORT=SORT.extract(1,SORT.length()-1)
		}

	if WHERE="" set vudwhere="%LIBS='SYSDEV' AND FID=:FID"
	else  set vudwhere="("_WHERE_") AND (%LIBS='SYSDEV' AND FID=:FID)"

	do OPEN^SCAIO quit:ER

	type ResultSet tmpdqrs=Db.select("ELEMENT","TMPDQ","PID=:%ProcessID")

	set CNT=0
        while tmpdqrs.next() do { quit:ER
		type String FID

		// If interactive, prompt to continue
		if CNT,IO=$P do { quit:ER
			type String MSG

			set MSG=""
			if $$^DBSMBAR(161)'=1 set ER=1,RM=""
			}

		set FID=tmpdqrs.getCol("ELEMENT")
		set VRWOPT("NOOPEN")=1
		set VRWOPT("NOCLOSE")=1
		do @("V0^"_PGM)
		set CNT=CNT+1
		}

	do CLOSE^SCAIO
	
	do Db.delete("TMPDQ","PID=:%ProcessID")

	quit

	
private VALIDWHR	// Validate where clause entered

	// Entry must be a valid where clause against table DBTBL1D
	
	type public String RM, X
	
	set RM = ""
	
	quit:X.isNull()
	
	catch error {

		/* NOTE:  Can't pass ER back since it gets
		   caught in the error handling logic with
		   vOpen0 in the generated code, so deal with
		   it in the post-processor code.
		*/

		// Invalid WHERE statement
		set RM = $$^MSG(1507)

	}
	// Try it ... if it fails it will trigger the catch block
	#ACCEPT Date=09/19/06; Pgm=RussellDS; CR=22567
	type ResultSet rs = Db.select("DI", "DBTBL1D", X)
	
	quit
	
 #OPTION ResultClass ON
public String vSIG()	quit "60527^73806^Dan Russell^2789"	// Signature - LTD^TIME^USER^SIZE
