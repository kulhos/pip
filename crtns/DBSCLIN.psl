	/*
	ORIG: giridhab - 06/08/2005
	DESC: CALL IN STUB FOR COMPUTEDS

	---- Comments --------------------------------------------------------
	This procedure is the call-in stub to support computed data item operation
	in a relational database environment. CALFRMC is called by an oracle process
	passing in the arguments that are reqired for the computed data item to be 
	evaluated by value.
	
	LINETAG:
		CALFRMC
	INPUTS:
		STR1 - STRING WITH EXTRINSIC TO BE CALLED (AGE^CIFCDI)
		STR2 - ARGUMENT LIST SEPERATED BY $C(31).
		
	RETURNS:
		RET  - RETURN VALUE OF THE COMPUTED DATA ITEM
	

	---- Revision History ------------------------------------------------
	11/09/2008 - RussellDS - CRs 35741/36391
		Added missing quit to main section.
	
	10/01/2008 - RussellDS - CRs 35714/35918
		Modified error handling to use new logging syntax.
		
		Removed old revision history.
	
	 03/20/07 - GIRIDHARANB - CR 26364
	 	    Modified to add an additional parameter/processing that is used 
	 	    to define certain system variables like %UID and %UCLS
	*/
	
	quit
	

public CALFRMC(str1,str2,str3) // handle call-in to evaluate computeds

	type String ARG,ET,PAR,RET,RM
	type Number I,ER

	#ACCEPT CR=23210;PGM=KELLYP;DATE=9/20/06
	#BYPASS
	kill (str1,str2,str3)
	#ENDBYPASS
	
        catch error@"LOGERROR" {
 
                set ER = 1
                set RM = error.description
	}

	do RUNC^ORACON
	if ER=-4 set ER=0,RM=""
	else  if ER quit Class.new("Error","Database connection error")

	for I=1:1:str2.length(31.char()) do {
		set PAR=str2.piece(31.char(),I)
		if PAR'=+PAR set str2.piece(31.char(),I)=PAR.addQuotes()
	}

	set str2=str2.translate(31.char(),",")

	// process user/terminal information in str3
	if str3.exists(), (str3 '= " ") do {
		
		type Number opt = str3.extract()
		type String ptoken=str3.extract(2,99)
		
		if opt=1 do EXECPID(ptoken) quit
		do EXECTKN(ptoken)
	}

	do SYSVAR^SCADRV0()
	set ARG="S RET="_str1_"("_str2_")"
	#ACCEPT DATE=07/11/04; CR=21139; PGM=Badri Giridharan
	xecute ARG
	
	quit RET

EXECTKN(ptoken)

	type Public String %UID,%UCLS
	type RecordTOKEN tkn =Db.getRecord("TOKEN","TOKEN=:ptoken",1)
	
	if 'tkn.getMode() do {  quit
		set (%UID,%UCLS)=""
	}
	
	set %UID=tkn.UID
	set %UCLS=tkn.UCLS

	quit

EXECPID(ptoken)

	type public String drvvars, %UID, %UCLS
	type RecordSAVEDRV savedrv=Db.getRecord("SAVEDRV","JOBNUM=:ptoken",1)
	
        if 'savedrv.getMode() do {  quit
                set (%UID,%UCLS)=""
        }

	set drvvars=savedrv.drvvars

	#ACCEPT DATE=03/19/07; CR=24702; PGM=Badri Giridharan; GROUP=XECUTE;
	XECUTE drvvars

	quit
	
	
public LOGERROR(Error err)	// Log error

	if (err.component '= "%GTM") do {
	
		type String ET
		set ET = err.type_"-"_err.thrownAt
		do ^UTLERR
	}
	else  do LOGERR^UTLERR(err)
	
	quit
 #OPTION ResultClass ON
public String vSIG()	quit "61311^54346^Dan Russell^2648"	// Signature - LTD^TIME^USER^SIZE
