DBSINDX	// Index File Definition
	/*
	     ORIG:  CHIANG -  5 JUN 1991
	     DESC:  Index File Definition
	  I18N=QUIT: Excluded from I18N standards.
	
	---------- Revision History -------------------------------------------
	01/21/2008 - RussellDS - CR30801
		Removed reference to DBTBL1.UDFILE
		
		Modified scoping of ER to make public to avoid lost error
		handling on return.
	
	10/10/2007 - RussellDS - CR29695
		Eliminate use of setMode(3).  Being obsoleted in PSL.
	
	07/30/07 - RussellDS - CR28507
		   Fixed MODIFY1 section to deal with deletion of a single
		   index.  It was not working previously.
		   
		   Removed old revision history.
	-----------------------------------------------------------------------
	*/
	
	quit
	
	
CREATE	// Create Index File Definition

	type public Number ER
	
	type String FID, VFMQ
	
	set FID=""
	set VFMQ=""
		
	do PFILES(0,.FID,.VFMQ) 

	if VFMQ="Q" quit
	if ER quit
	
	do MODIFY1(FID)
	
	quit
	
	
	
MODIFY	// Modify Index File Definition
	
	type public Number ER
	
	type String FID, VFMQ
	
	set FID=""
	set VFMQ=""
	
	do PFILES(1,.FID,.VFMQ) 

	if VFMQ="Q" quit
	if ER quit
	
	do MODIFY1(FID)
	
	quit
	
MODIFY1( String FID)	//

	type Boolean DELFLG
	type String INDEXNM, VFMQ
	
	set %ProcessMode = 1
	set INDEXNM = " "
	set DELFLG = false
	
	type RecordDBTBL1 DBTBL1 = Db.getRecord("DBTBL1","%LIBS='SYSDEV',FID=:FID")
	type RecordDBTBL8 DBTBL8 = Class.new("RecordDBTBL8")

	set DBTBL8.%libs = "SYSDEV"
	set DBTBL8.fid = FID
	
	do DRV^USID(0,"DBTBL8",.DBTBL8,.DBTBL1)
	
	if VFMQ = "Q" quit
	
	if DELFLG do {
		
		do Db.delete("DBTBL8", "%LIBS='SYSDEV' AND FID=:DBTBL8.FID AND INDEXNM=:DBTBL8.INDEXNM")
	}
	else  do DBTBL8.save()	// Call filer to update record
	
	// Index name ~p1 filed ... Continue?
	if '$$YN^DBSMBAR("",$$^MSG(1218,INDEXNM),1) quit

	do MODIFY1(FID)
	
	quit
	
	
MODE	// Determine processing mode	

	type public Number %ProcessMode, DELFLG

	if DELFLG set %ProcessMode=3 quit			// Delete mode

	if Db.isDefined("DBTBL8","%LIBS='SYSDEV',FID=:FID,INDEXNM=:INDEXNM") set %ProcessMode = 1 quit  // Modify mode

	set %ProcessMode = 0			// Create mode
	
	quit
	
	
PFILES(	Number IOPT,
	String FID,
	String VFMQ)	// Select primary file name

	type public Number ER
	type public String RM
	
	type Boolean IEXIST
	type String srclib
	
	set FID=$$FIND^DBSGETID("DBTBL1",0) 

	if FID="" set VFMQ="Q" quit
	
	set VFMQ="F"

	type ResultSet rs = Db.select("INDEXNM","DBTBL8","%LIBS='SYSDEV' AND FID=:FID")
	if rs.isEmpty() set IEXIST = 0
	else  set IEXIST = 1
	
	// Index file definition already exists
	if 'IOPT,IEXIST set ER=1 set RM=$$^MSG(1214) quit
	
	// Index file definition not available
	if IOPT,'IEXIST set ER=1 set RM=$$^MSG(1215) quit
	
	quit
	
	
COPY	// Copy Index File Definition
	
	type public Number ER
	type public String RM
	
	type String %TAB(), %READ, %NOPRMT, FID, INDEXNM, PSFILE, VFMQ
	
	set ER = 0
	set RM = ""
	
	set %TAB("PSFILE")=".PSFILE1/TBL=[DBTBL1]"
	set %TAB("FID")=".POFILE1/TBL=[DBTBL1]/XPP=D COPYPP^DBSINDX"
	
	set %READ="@@%FN,,,PSFILE/REQ,FID/REQ",%NOPRMT="F"

	do ^UTLREAD 
	
	if VFMQ="Q" quit
	
	type DbSet ds = Db.selectDbSet("DBTBL8","%LIBS='SYSDEV' AND FID=:PSFILE")
	while ds.next() do {

		type RecordDBTBL8 FD8 = ds.getRecord("DBTBL8")
		type RecordDBTBL8 TD8 = Class.new("RecordDBTBL8")

		set TD8 = FD8.copy()
		set TD8.fid = FID

		do TD8.save()

	}
	
	write $$MSG^%TRMVT($$^MSG(855),"",1)
	
	quit
	
	
	
COPYPP	// -------- Post-Processor
	
	type public Number ER
	type public String RM, X
	
	quit:X.isNull()
	
	type ResultSet rs = Db.select("INDEXNM","DBTBL8","%LIBS='SYSDEV' AND FID=:X")

	// Already created
	if 'rs.isEmpty() set ER=1 set RM=$$^MSG(252)

	quit
	
	
	
	
DELETE	// Delete Index File Definition
	
	type public Number ER
	
	type String FID, VFMQ
	
	set ER = 0
	set FID = ""
	set VFMQ = ""
	
	do PFILES(1,.FID,.VFMQ) 
	
	quit:VFMQ="Q" 
	quit:ER
	
	// Are you sure?
	if '$$YN^DBSMBAR("",$$^MSG(307)) quit

	do Db.delete("DBTBL8","%LIBS='SYSDEV' AND FID=:FID")

	write $$MSG^%TRMVT($$^MSG(855),"",1)

	quit
 #OPTION ResultClass ON
public String vSIG()	quit "61247^57696^Dan Russell^3802"	// Signature - LTD^TIME^USER^SIZE
