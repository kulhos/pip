SCADRV8	//

  /*
ORIG: chhabris - 08/01/2005
DESC: CREATE/MODIFY/DELETE NEW MENU

---- Comments --------------------------------------------------------



---- Revision History ------------------------------------------------

	08/01/2005 - CHHABRIS - CR16677
		     Converted to PSL.
		     Also removed the revision history prior to 2004.
		     
----------------------------------------------------------------------

 */

	quit

NEW	//
	set %ProcessMode=0 do INIT quit

UPD	//
	set %ProcessMode=1 do INIT quit

DEL	//
	set %ProcessMode=3 do INIT quit

INIT	//

	type public String ER,MENU,MENU1,OLNTB,%FN,%READ,%TAB,RM,VFMQ
	type public Boolean %NOPRMT

	kill OLNTB

	set %TAB("MENU")=".MENU1/TBL=[SCAMENU0]/XPP=D PPMENU^SCADRV8"
	set %READ="@@%FN,,,MENU/REQ",%NOPRMT="F"

	do ^UTLREAD

	// No changes made
	if VFMQ="Q"!'MENU.data() set ER="W",RM=$$^MSG(1905) quit
	
	do VPG1
	quit
	

VPG1	//

	type public Number DEL,DEL1,NI,X
	type public String DESC,MENU,PROMPT,%READ,%TAB,RM,VFMQ
	type public Boolean %NOPRMT
	type String HDG,HDG2

	set (DESC,PROMPT)=""

	// Menu Number ~p1
	set HDG=$$^MSG(5484,MENU),HDG=$J("",(80-(HDG.length()))\2)_HDG

	// Enter ~p1 function
	set %TAB("DESC")=".DESC2/XPP=if PROMPT="""" set PROMPT=$$^MSG(8370,X),RM=PROMPT_%_(NI+1)"
	set %TAB("PROMPT")=".PROMPT1"

	type RecordSCAMENU0 scamenu0 = Db.getRecord("SCAMENU0","MENU=:MENU",1)
	if scamenu0.getMode() set DESC=scamenu0.des,PROMPT=scamenu0.prmpt

	set DEL=0

	set %READ="@@%FN,,,DESC/REQ,PROMPT/REQ"

	// Description: ~p1
	if %ProcessMode=3 set %TAB("DEL")=".DEL1",HDG2=$$^MSG(8233,DESC),%READ="@HDG,,@HDG2,DEL/REQ",DEL=1,%NOPRMT="F"
	do ^UTLREAD

	if %ProcessMode=3,'DEL set VFMQ="Q"

	do VER
	quit


VER	//
	type public String VFMQ

	if VFMQ="Q" do END quit
	do FILE

	if %ProcessMode=0 do EXT^SCADRV5

	do END
	quit


FILE	//
	type public String DESC,MENU,PROMPT

	if %ProcessMode=3 do Db.delete("SCAMENU0","MENU=:MENU") quit

	type RecordSCAMENU0 scamenu0=Db.getRecord("SCAMENU0","MENU=:MENU",1)
	set scamenu0.des=DESC
	set scamenu0.prmpt=PROMPT
	do scamenu0.bypassSave()
	quit


END	//
	type public String ER,MENU,RM,VFMQ

	if VFMQ="Q" do {

		// Menu ~p1 not created
		if %ProcessMode=0 set RM=$$^MSG(1711,MENU) quit

		// Menu ~p1 not modified
		if %ProcessMode=1 set RM=$$^MSG(1713,MENU) quit

		// Menu ~p1 not deleted
		set RM=$$^MSG(1712,MENU)
		}

	else  do {

		// Menu ~p1 created
		if %ProcessMode=0 set RM=$$^MSG(1708,MENU) quit

		// Menu ~p1 modified
		if %ProcessMode=1 set RM=$$^MSG(1710,MENU) quit

		// Menu ~p1 deleted
		set RM=$$^MSG(1709,MENU)
		}

	set ER="W"
	quit


PPMENU	//
	type public String I(),RM
	type public Number ER,%OSAVE,X
	type String FMENU,M,N,SUBM

	// MENU post processor
	if '%OSAVE set I(3)=""

	type ResultSet rs=Db.select("MENU","SCAMENU0","MENU=:X")

	// Record already exists
	if '%OSAVE,'X.isNull(),'rs.isEmpty() set ER=1,RM=$$^MSG(2327)

	if %OSAVE'=3 quit

	type ResultSet rs1=Db.select("SNUMB","SCAMENU","MNUMB=:X")

	// Menu not empty
	if 'X.isNull(),'rs1.isEmpty() set ER=1,RM=$$^MSG(1705) quit

	// Check other menus and sub-menus
	set (N,M)=""
	set ER=0

	type ResultSet rs2=Db.select("MENU","SCAMENU0")
	while rs2.next() do {
		set N=rs2.getCol("MENU")
		type ResultSet rs3=Db.select("SNUMB,FUNMENU","SCAMENU","MNUMB=:N")
		while rs3.next() do {
			set M=rs3.getCol("SNUMB")
			set FMENU=rs3.getCol("FUNMENU")
			if FMENU'=X quit

			// Menu is linked to menu #~p1
			set ER=1,RM=$$^MSG(1704,N)
			}
		}
	quit:ER

	type ResultSet rs4=Db.select("FN","SCATBL4")
	while rs4.next() do {
		set N=rs4.getCol("FN")
		type ResultSet rs5=Db.select("SEQ,SUB","SCATBL4","FN=:N")
		while rs5.next() do {
			set M=rs5.getCol("SEQ")
			set SUBM=rs5.getCol("SUB")
			if SUBM.translate("@")'=X quit
			}
		}
	quit
 #OPTION ResultClass ON
public String vSIG()	quit "60218^26329^Sanjay Chhrabria^3557"	// Signature - LTD^TIME^USER^SIZE
