public DBSRWBNR(String IO,String BNRINFO(,))	/* Part DBSRW*
	ORIG: CHIANG - 8/8/88
	PROCEDURE ID: DBSRWBNR
	DESC: DATA-QWIK Report Banner Print
	
	---- Comments --------------------------------------------------------
	ARGUMENTS:
		. IO		Output device		/REQ/NONULL/MECH=VAL

		. BNRINFO	Information used	/REQ/MECH=REFARR:R
				for banner printing

	------ Revision History ------------------------------------------------
	10/15/2008 - RussellDs - CR35918
		Corrected PSL warnings in generated code.	
	
	02/08/04 - RussellDS - CR13817
		   Make public to avoid PSL warning errors.
		   
		   Fix miscellaneous PSL warnings.

	12/09/03 - Dan Russell - CR 7239
                   Fix PSL warnings for operator precedence.

	12/17/02 - Dan Russell - 51351
		   Moved into PSL.  Removed old change history.

		   This code was originally in routine DBSEXEBN

	------------------------------------------------------------------------

	I18N=QUIT Excluded from I18N standards
	*/

	type Number N
	type String ALLEQLS,DESC,LINE,RID,TABLOC

	set DESC=BNRINFO("DESC").get()

	set ALLEQLS=""
	set ALLEQLS.piece("=",DESC.length()+1)=""	// ===== line
	set TABLOC=(75-DESC.length())\2			// Tab location
	set LINE=""
	set LINE.piece("-",71)=""			// ----- line

	// Description
	use IO write !,?TABLOC,ALLEQLS,!,?TABLOC,DESC,!,?TABLOC,ALLEQLS,!!!

	// Documentation
	if BNRINFO("DOC","").order() do {
		type Number I,N

		set N=""
		write !?5,LINE
		for I=1:1 set N=BNRINFO("DOC",N).order() quit:N.isNull()  write !?5,BNRINFO("DOC",N)
		write !?5,LINE,!
		if I>21 write 12.char(),!			// New page
		}

	write !,"User: ".justify(20),%UserID
	write ?45,"Run: ",%CurrentDate.toString(),"  ",$$TIM^%ZM,!
	if %SystemDate.get() write ?42,"System: ",%SystemDate.toString(),!
	write !

	set RID=BNRINFO("RID")
	if RID.extract(1,5)="QWIK_" do {			// QWIK report
		write "QWIK Report: ".justify(20)
		write RID.extract(6,RID.length())
		}
	else   write "Report: ".justify(20),RID
	
	write,?41,"Program: ",BNRINFO("PGM").get()

	// File(s): 
	write !!,$$^MSG(3479).justify(20),BNRINFO("TABLES").get()

	write !!,"Order By: ".justify(20)
	do {
		type Number N=""
		type String X

		for  set N=BNRINFO("ORDERBY",N).order() quit:N.isNull()  do {
			set X=BNRINFO("ORDERBY",N)
			write ?20,$$DES^DBSDD(X),!
			}
		}

	if BNRINFO("PROMPTS").data() do {
		type Number N=""
		type String X

		write !!,"Input: ".justify(20)
		for  set N=BNRINFO("PROMPTS",N).order() quit:N.isNull()  do {
			set X=BNRINFO("PROMPTS",N)
			write ?20,X.piece("|",2)," "
			write X.piece("|",4,99),!
			}
		}

	// Break WHERE clause into 60 character chunks to pring
	write !!,?5,LINE.extract(1,31)," WHERE ",LINE.extract(1,32),!!
	if 'BNRINFO("WHERE").get().isNull() do {
		type Number I
		type String X

		set X=BNRINFO("WHERE")
		for  do { quit:X.isNull()
			if X.length()<60 do {
				write ?10,X,!
				set X=""
				}
			else  do {
				for I=60:-1:1 quit:X.extract(I)=" "
				write ?10,X.extract(1,I),!
				set X=X.extract(I+1,X.length())
				}
			}
		}
	else  write ?32,"No WHERE clause",!

	write !,?5,LINE,!!

	quit
 #OPTION ResultClass ON
public String vSIG()	quit "61288^63986^Dan Russell^2944"	// Signature - LTD^TIME^USER^SIZE
