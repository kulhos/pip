DBSRWALN(String RID,String IO,Number COUNT)	/* Private - Part DBSRW*
	ORIG: CHIANG - 8/16/88
	PROCEDURE ID: DBSRWALN
	DESC: DATA-QWIK Report Alignment Print
	
	---- Comments --------------------------------------------------------
	ARGUMENTS:
		. RID		Report ID		/REQ/NONULL/MECH=VAL

		. IO		Output device		/REQ/NONULL/MECH=VAL

		. COUNT		Alignment cound		/REQ/NONULL/MECH=VAL

	------ Revision History ------------------------------------------------
	
	2009-03-11 - RussellDS - CRs 35741/38660
		* Modified to replace $C(128) with "|" when load prompts.  128
		  is substituted by FORMDQ5* to avoid problems with "|" in
		  the record.

        07/26/05 - SkariahV- CR16679
	           Removed #WARN directive.
	              
	12/16/02 - Dan Russell - 51351
		   Moved into PSL.  Removed old change history.

		   This code was originally in routine DBSEXEAL

	------------------------------------------------------------------------

	I18N=QUIT Excluded from I18N standards
	*/

	type Number FROM,I,RPTCNT,RPTFROM,RPTSIZE,RPTTO,TO
	type String ALL9,ALLX,GRP,PRINT()

	use IO write !

	// Find last key level with detail
	type RecordDBTBL5H dbtbl5h=Db.getRecord("DBTBL5H","LIBS=""SYSDEV"",RID=:RID")
	set GRP=dbtbl5h.seq10 if $$GRPDTL(RID,GRP,.FROM,.TO)
	else  set GRP=dbtbl5h.seq9 if $$GRPDTL(RID,GRP,.FROM,.TO)
	else  set GRP=dbtbl5h.seq8 if $$GRPDTL(RID,GRP,.FROM,.TO)
	else  set GRP=dbtbl5h.seq7 if $$GRPDTL(RID,GRP,.FROM,.TO)
	else  set GRP=dbtbl5h.seq6 if $$GRPDTL(RID,GRP,.FROM,.TO)
	else  set GRP=dbtbl5h.seq5 if $$GRPDTL(RID,GRP,.FROM,.TO)
	else  set GRP=dbtbl5h.seq4 if $$GRPDTL(RID,GRP,.FROM,.TO)
	else  set GRP=dbtbl5h.seq3 if $$GRPDTL(RID,GRP,.FROM,.TO)
	else  set GRP=dbtbl5h.seq2 if $$GRPDTL(RID,GRP,.FROM,.TO)
	else  set GRP=dbtbl5h.seq if '$$GRPDTL(RID,GRP,.FROM,.TO) quit

	set ALL9="",ALL9.piece("9",31)=""
	set ALLX="",ALLX.piece("X",121)=""

	// Set up print buffers
	type RecordDBTBL5DGC dbtbl5dg=Db.getRecord("DBTBL5DGC","LIBS=""SYSDEV"",RID=:RID,GRP=:GRP")

	set RPTFROM=dbtbl5dg.rptfrom
	set RPTTO=dbtbl5dg.rptto
	set RPTSIZE=dbtbl5dg.rptsize
	set RPTCNT=dbtbl5dg.rptcnt

	for I=FROM:1:TO set PRINT(I)=""

	type DbSet ds=Db.selectDbSet("DBTBL5D","LIBS='SYSDEV' AND RID=:RID AND GRP=:GRP","ITMSEQ ASC")
	while ds.next() do {
		type Number LINE,SIZE,TAB
		type String BLANK,OUT,TYPE,VAR
		type RecordDBTBL5D dbtbl5d=ds.getRecord("DBTBL5D")

		set LINE=dbtbl5d.status\1000
		set TAB=dbtbl5d.status#1000

		quit:LINE<FROM		// Group header
		quit:LINE>TO		// Group trailer

		set SIZE=dbtbl5d.size
		set TYPE=dbtbl5d.fmt
		set VAR=dbtbl5d.prompt.translate(128.char(),"|")	// Restore up-bar

		if dbtbl5d.item?1"@".E,VAR["," do {		// Convert type and size
			type Number I

			for I=1:1:10 quit:VAR.piece(",",I)=""
			if VAR.piece(",",I-1) set SIZE=VAR.piece(",",I-1)
			set TYPE=VAR.piece(",",I-2)
			}

		// Set up to print 99999 or XXXX  based on format type

		if TYPE="N" set OUT=ALL9.extract(1,SIZE)
		else  if TYPE="$"!(TYPE="E") set OUT=ALL9.extract(1,SIZE-3)_".99"
		else  if TYPE="T",VAR'?1"@".E,dbtbl5d.name?1"@".E,VAR'?1"<<".E set OUT=VAR

		// <<VAR,D,SIZE>>
		else  if VAR?1"<<"1E.E1">>",VAR.piece(",",2)="D" set OUT="MM/DD/YY"

		// @CHR
		else  if dbtbl5d.name?1"@".E,VAR?1"@CHR(".E do {
			type Number I
			type String X

			set X=VAR.piece(",",1).piece("(",2)
			set OUT=""
			for I=1:1:SIZE S OUT=OUT_X
			}

		else  set OUT=ALLX.extract(1,SIZE)

		set BLANK=$J("",TAB-1-PRINT(LINE).length())
		set PRINT(LINE)=PRINT(LINE)_BLANK_OUT
		}

	for I=1:1:COUNT do {			// Output buffer information
		type Number J,K,LINE

		set LINE=""
		for J=1:1 set LINE=PRINT(LINE).order() quit:LINE=""  do {
			if LINE<RPTFROM!(LINE>RPTTO) write PRINT(LINE),!
			else  do {				// Repeat Group
				type String BLANK
				set BLANK=$J("",RPTSIZE-PRINT(LINE).length())
				for K=1:1:RPTCNT write PRINT(LINE)_BLANK
				write !
				}
			}
		}
	quit


GRPDTL(String RID,	// Private - Check detail status at this level
	String GRP,
	Number FROM,
	Number TO)

	type Number DETAIL=0

	quit:GRP="" 0

	type RecordDBTBL5DGC dbtbl5dg=Db.getRecord("DBTBL5DGC","LIBS=""SYSDEV"",RID=:RID,GRP=:GRP")

	set FROM=dbtbl5dg.reginfo.curVal.piece(",",1)+2
	set TO=dbtbl5dg.reginfo.curVal.piece(",",2)+FROM-1

	type ResultSet rs=Db.select("STATUS","DBTBL5D","LIBS='SYSDEV' AND RID=:RID AND GRP=:GRP")
	while rs.next() do { quit:DETAIL
		type Number X

		set X=rs.getCol("STATUS")\1000
		if X'<FROM,X'>TO set DETAIL=1
		}

	quit DETAIL
 #OPTION ResultClass ON
public String vSIG()	quit "61431^63999^Dan Russell^4282"	// Signature - LTD^TIME^USER^SIZE
