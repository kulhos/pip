public UPID(String FID,String PGM)	//Data item protection utility
	/*
	ORIG:  BOB CHIANG (8447) - 12/30/87

	Return Data Item Protection Program Name VPnnXXXX
	
	KEYWORDS: DATA-QWIK, Screen Handling
	
	ARGUMENTS:
	   . FID	DATA-QWIK File ID			/TYP=T/REQ/MECH=VAL
	
	   . PGM	Protection program name			/TYP=T/MECH=REFNAM:W
			Run-time program name format is VPllXXXX
			where ll=library #,  XXXX=file id
	
	OUTPUT:
	   . PGM	Protection program name			/TYP=T
			Run-time program name format is VPllXXXX
			where ll=library #,  XXXX=file id
	
	   . RM		Return error message			/TYP=T/COND=(ER=1)/DFT=null
	
	EXAMPLES:

	    S FID="DEP" D ^UPID(FID,.PGM)  returns  PGM="VP01DEP"
	
	---------- Revision History -------------------------------------------
	
	01/17/06 - RussellDS - CR19089
		   Correct syntax in EXT section for call to protection program.
	
	12/19/05 - RussellDS - CR18400
		   Fix error message returned from top section.

        07/26/05 - SkariahV- CR16679
	           Removed #WARN directive.
	              
	03/21/05 - RussellDS - CR14908
		   Modified EXT section to accept an object and pass it on
		   to %EXT of the protection program.  And, to pass VP as a
		   parameter, versus just a public array.
		   
		   Add public labels to all tags.

	02/06/03 - Dan Russell - 51349
		   Fixed problem in PGM section to allow to always return name
		   if valid.

	01/08/03 - Dan Russell - 51349
		   Fix undefined error. Clean up code.

	04/18/01 - ARCILLAZ - 43583
                   Convert to PSL. Removed old revision history.

	-----------------------------------------------------------------------
	*/

	type Public Number ER=0
	type Public String RM

	type String FPN,MPLCTDD,X,ZFID,ZLIBS

	set (PGM,RM)=""

	type ResultSet rs=Db.select("DISTINCT FID","DBTBL14","FID=:FID")
	quit:rs.isEmpty()

	// Get protection program FILE ID from file definition
	type RecordDBTBL1 dbtbl1=Db.getRecord("DBTBL1","%LIBS=""SYSDEV"",FID=:FID",1)
	// Invalid file name ~p1
	if 'dbtbl1.getMode() set RM=$$^MSG(1337) quit

	set FPN=dbtbl1.fpn

	// Protection program name not set up for file ~p1
	if FPN="" do Runtime.setErrMSG("DBTBL1",2279, FID) quit:ER

	set PGM="VP01"_FPN
	quit 
	

public STATUS(String FID,	// Public - Function to return protection definiton status
	String DINAM,
	Number FLG)
	/*
	ARGUMENTS:
	   . FID	data file			/REQ/NONULL
	
	   . DINAM	data item name			/REQ/NONULL
	
	   . FLG	protection status		/MECH=REFNAM:W
	                0 - No protection definition
	                1 - With protection definitions
	
	EXAMPLES:

	   D STATUS^UPID("DEP","BAL",.FLG)  returns FLG=1
	*/

	set FLG=0

	if Db.isDefined("DBTBL1","%LIBS=""SYSDEV"",FID=:FID") do {

		// Any data item
		if DINAM="#" do {
			type ResultSet rs=Db.select("DISTINCT FID","DBTBL14","FID=:FID")
			if 'rs.isEmpty() set FLG=1
			}

		// Specific data item
		else  do {
			type ResultSet rs=Db.select("DINAM","DBTBL14","PLIBS='SYSDEV' AND FID=:FID AND DINAM=:DINAM")
			if 'rs.isEmpty() set FLG=1
			}
		}

	quit


public EXT(String FID,		// Table name
	   Record REC)		// Record object
    
	/*
	External entry point for data item protection user interface
	
	INPUT:
	   . System	%UCLS   userclass		/TYP=T/REQ

	   . keys	access keys for			/TYP=T/REQ
			file FID
	
	   . VP		Data item name array		/TYP=ARY/REQ
			( Input & Output )
			Input  = VP(DI)=""
	
	RETURNS:
	   . VP		Data item name array		/TYP=ARY
			Output = VP(DI)=protection 
			status by %UCLS
	
	   . RM		Return Error Message		/TYP=T/COND
	
	
	EXAMPLES:
	    -----------------------------------------
	    check protection status of data item
	    BAL & BOO in file DEP for account # 1
	    -----------------------------------------
	                                                 Output variables
	                                                 =====================
	   S %UCLS="MGR",FID="DEP"                     |
	   S CID=1                                     |
	   S VP("BOO")="",VP("BAL")=""                 |
	   D EXT^UPID(FID)                             |
	                                               | VP("BAL")=2,VP("BOO")=""
	
	-----------------------------------------------------------------------
	*/

	type Public String RM, VP()

	type Number FLG
	type String PGM
	
	// Name not defined
	do UPID(FID,.PGM) if PGM="" set RM="" quit

	// No definition
	do STATUS(FID,"#",.FLG) if 'FLG set PGM="" quit

	do @("%EXT^"_PGM_"(.REC,.VP)")
	
	quit 

	
public PGM(String FID)	/* Public - Return program name

	Legacy function -- calling at top would do as well

	ARGUMENTS:
		. FID		File Name	/NOREQ
	*/

	type String FPN,PGM

	if FID.get()="" quit ""

	// Get protection program FILE ID from file definition
	type RecordDBTBL1 dbtbl1=Db.getRecord("DBTBL1","%LIBS=""SYSDEV"",FID=:FID",1)
	set FPN=dbtbl1.fpn

	if FPN="" quit ""

	set PGM="VP01"_FPN

	quit PGM
 #OPTION ResultClass ON
public String vSIG()	quit "60282^65131^Dan Russell^4639"	// Signature - LTD^TIME^USER^SIZE
