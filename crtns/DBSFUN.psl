DBSFUN	
		 
	/*
	DATA-QWIK Internal Functions
	
	NOTE:  The only remaining function here is SELDI.  
		    
	LIBRARY:
		$$SELDI		- Set up lookup table syntax

	---- Revision History -------------------------------------------------
	04/28/2008 - RussellDS - CR33611
		Modified SELFID to correct return of incorrect file on
		selection against multiple files.
	
	12/14/05 - RussellDS - CR18400
		   Converted to PSL.

	-----------------------------------------------------------------------
	*/
	
	quit			// Don't call from top
	

public SELDI(String FILES,	// Access files (primary,secondary,...)
	     String X)		// Input table syntax [*]		//NOREQ
	
	/*
	Set up lookup table syntax for any DQ related data item name
	selection

	ARGUMENT NOTES:
		. X	Valid options:

			  NULL     select item from one of the access file
			  DI       match data item from any access file
			  [FID]    select data item from file FID
			  [FID]DI  match DI from entry in the file FID

	Example:
	            Enter following MUMPS code in user defined data item
	            pre-processor:
	
	            S FILES=...                         ; Define var FILES
	            S I(3)="@SELDI^DBSFUN(FILES,.X)"    ; lookup table init
	            Q                                   ; entry point
	
	            Change table selection X to [FID]X syntax in user
	            defined data item post-processor.
	
	            I X'="",X'["[" S X="["_$G(zztblfid)_"]"_X
	*/
	
	type public Boolean ER = 0
	type public String vINFO(), vREF, zztblfid
	
	type String FID, HDG, LIB

	kill zztblfid
	
	quit:FILES.get().isNull()
	
	set X = X.get().piece("/", 1)
	
	if X.isLike("%.%") do {
		
		set FID = X.piece(".", 1)
		set X = X.piece(".", 2)
	}
	else  if X.beginsWith("[") do {
		
		set FID = X.piece("]", 1).piece("[", 2)
		set X = X.piece("]", 2)
	}
	else  set FID = $$SELFID(FILES, .X)
	
	if FID.isNull() set ER = 1 quit

	// Define lookup table display format and collating method
	set vREF = "[DBTBL1D]DI/LE=12/RH=Name,DES/LE=37/RH=Description,TYP/LE=1/RH=Type,LEN/LE=5/RH=len,NOD/LE=7/RH=Field,POS/LE=2/RH=Pos,USER/LE=4/RH=User:QU ""[DBTBL1D]FID="""_FID_""""""
	set HDG = "Name          Description ["_FID_"]"
	set HDG = HDG.justify(52, -1)_" Type Size Field   Pos  User"
	set vINFO("HDG") = HDG
	
	//  zztblfid can be used by post-proc to change table entry X TO [zztblfid]X
	set zztblfid = FID
	
	quit


SELFID(String FILES,	// Access files
       String DI)	// Column name
       
	/*
	Select file name from access file list FILES
	*/
	
	type Number CNT, I, OP
	type String FID, FIDS(), VAR()

	if 'FILES.isLike("%,%") quit FILES		// Single file
	
	for I = 1:1:FILES.length(",") do {
			
		set FID = FILES.piece(",", I)
		
		if DI.isNull() set FIDS(FID) = ""
			
		else  if Db.isDefined("DBTBL1D", "%LIBS='SYSDEV' AND FID=:FID AND DI=:DI") set FIDS(FID) = ""
	}
	
	set CNT = 0
	set FID = ""
	for  set FID = FIDS(FID).order() quit:FID.isNull()  do {
		
		set CNT = CNT + 1
		set VAR(CNT) = FID
	}
	
	if (CNT = 1) quit VAR(CNT)			// Single match	

	set OP = $$^DBSMBAR(20, "", "", "", .VAR())
	
	if (+OP = 0) quit ""
	
	quit VAR(OP)
 #OPTION ResultClass ON
public String vSIG()	quit "61114^71587^Dan Russell^2985"	// Signature - LTD^TIME^USER^SIZE
