	/* 
	ORIG: DOUGANM - 02/02/2000
	DESC: Computed data items for SCAU file
	
	LIBRARY:
	$$STATUS User Status
	
	---- Revision History ------------------------------------------------

        07/26/05 - SkariahV- CR16679
	           Removed #WARN and #OPTIMIZE directives.
	              
	06/23/03 - Erik Scheetz - 51349
		   Defined linetags STATUS and SREASON as public.  Also
		   modified the parameters to each to meet Computed DI 
		   standards.

	06/26/02 - Erik Scheetz - 43583
		   PSL conversion cleanup.  Removed getOneRow methods.

	02/02/00 - DOUGANM - 37421
		   Converted code from M into PSL language.

	----------------------------------------------------------------------
	*/
	quit


public	STATUS(String UCLS,
		Date LSGN,
		Boolean MRSTAT,
		Number PWDFAIL)

	/*
	----------------------------------------------------------------------
	Computed Data Item [SCAU]STATUS

	This label is called when the SCAU.STATUS data item is accessed
	to retrieve the current status.

	Returns:	
		1 - Active
	   	2 - Inactive
	   	3 - Revoked
	----------------------------------------------------------------------
	*/
	new IDLE
	
	if MRSTAT quit 3			// Manually Revoked
	
	if $$PWDFAIL(UCLS,PWDFAIL) quit 3	// Revoked due to Password Failure
	
	set IDLE=$$EXPIRED(UCLS,LSGN)
	if IDLE=2 quit 3		// Revoked due to Inactivity
	if IDLE quit 2			// Set Status to Inactive
	
	quit 1				// Active Status


public SREASON(String UCLS,
		Date LSGN,
		Boolean MRSTAT,
		Number PWDFAIL)

	/*
	----------------------------------------------------------------------
	Computed Data Item [SCAU]SREASON

	This label is called when the SCAU.SREASON data item is accessed
	to retrieve the User Status Reason.

	Returns:
	 	1 - Password Failure
	    	2 - Inactivity
	   	3 - Manually Assigned
	----------------------------------------------------------------------
	*/

	if $$PWDFAIL(UCLS,PWDFAIL) quit 1
	if $$EXPIRED(UCLS,LSGN) quit 2
	if MRSTAT quit 3

	quit ""


PWDFAIL(String UCLS,Number PWDFAIL)
	/*
	----------------------------------------------------------------------
	Determines if the Maximum # of password retries has been reached.

	Returns:
	 	0 - Password failures limit NOT reached
	 	1 - Password failures limit reached
	----------------------------------------------------------------------
	*/

	if UCLS.isNull() quit 0

	type RecordSCAU0 scau0=Db.getRecord("SCAU0","UCLS=:UCLS")
	
	// Status checking turned off
	if 'scau0.pwdtry quit 0

	if PWDFAIL'<scau0.pwdtry quit 1
	quit 0


EXPIRED(String UCLS,Date LSGN)
	/*
	----------------------------------------------------------------------
	Determines if Maximum days before Revoked or Maximum days before 
	Inactive have been reached.

	Returns:
		0 - Still Active
		1 - Set to Inactive due to Inactivity
		2 - Set to Revoked due to Inactivity
	----------------------------------------------------------------------
	*/

	type Number IDLE
	
	// Quit if no User Class is set up or LSGN not populated (New User).
	if UCLS=""!(LSGN="") quit 0

	type RecordSCAU0 scau0=Db.getRecord("SCAU0","UCLS=:UCLS")

	if 'scau0.maxrev!('scau0.maxin) quit 0

	set IDLE=%CurrentDate-LSGN

	// Revoked
	if IDLE'<scau0.maxrev quit 2

	// Inactive
	if IDLE'<scau0.maxin quit 1

	// Active
	quit 0
 #OPTION ResultClass ON
public String vSIG()	quit "60107^24453^sviji^3096"	// Signature - LTD^TIME^USER^SIZE
