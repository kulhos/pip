SCAUSR	// Set up new Employee/User
	/*
	   ORIG: Frank R. Sanchez
	  ---- Revision History ------------------------------------------------

	   04/01/05 - KELLYP - CR 13666
	   	      Modified procedure to eliminate VPG* logic and to set up 
	   	      the user-related account arrays properly.  Also modified 
	   	      rest of procedure to conform to current PSL standards and 
	   	      removed pre-2003 revision history.

	   12/29/03 - CARROLLJ - CR7658
		      Corrected undefined error in N1 section.

	   06/22/03 - Erik Scheetz - 51349
		      Modified RESET section to call SCAUCDI pasig the correct 
		      parameters.

	  ---------------------------------------------------------------------
	*/

	quit


public NEW	// User Creation

	do INIT(0)
	quit

	
public UPD	// User Modification

	do INIT(1)
	quit


public INQ	// User Inquiry

	do INIT(2)
	quit 


public DEL	// User Deletion

	type public Boolean ER
	
	// Function disabled
	do Runtime.setErrMSG("SCAU","1129") quit:ER

	quit

	
INIT(Number %ProcessMode)

	type String CIFNAM(),IO,RELCIF(),UID,VFMQ

	do QUERY
	
	if VFMQ="Q" quit

	type RecordSCAU scau
	
	do SCREEN(.scau)
	
	do VER(.scau)

	quit


QUERY	// Query for User ID

	type String %READ,%TAB()

	set %TAB("UID")=".UID1/HLP=[SCAU]UID/XPP=D PPUID^SCAUSR"
	if %ProcessMode set %TAB("UID")=%TAB("UID")_"/TBL=[SCAU]"

	set %READ="@@%FN,,,UID/REQ"

	if %ProcessMode=2 do {
		set %TAB("IO")=$$IO^SCATAB($I)
		set %READ=%READ_",IO"
		}
	
	do ^UTLREAD 

	quit


PPUID	// User ID Post-Processor

	type public Boolean ER
	type public Number %OSAVE
	type public String X
	
	if X.isNull() quit
	if %OSAVE quit

	set X=X.upperCase()

	// Record already on file
	if Db.isDefined("SCAU","UID=:X") do Runtime.setErrSTBLER("SCAU","RECOF") quit:ER

	quit


SCREEN(RecordSCAU fSCAU)	// Call screen driver

	type public Boolean ER
	type public String IO,UID
	
	type Number %MODS,%REPEAT

	set %MODS=1
        set %REPEAT=16

	set fSCAU=Db.getRecord("SCAU","UID=:UID",1)

	if %ProcessMode do LOADRA
	if %ProcessMode=2,IO'=$I do OPEN^SCAIO quit:ER

	do DRV^USID(%ProcessMode,"SCAUSR1",.fSCAU)

	quit


LOADRA	// Load related account customer #'s and names

	type public String CIFNAM(),RELCIF(),UID

	type Number I

	set I=1

	type ResultSet rs=Db.select("SCAUR1.RECIF,CIF.NAM","SCAUR1,CIF","CIF.ACN=SCAUR1.RECIF AND SCAUR1.UID=:UID")
	while rs.next() do { quit:I>16
		set RELCIF(I)=rs.getCol(1)
		set CIFNAM(I)=rs.getCol(2)
		set I=I+1
		}

	quit


VER(RecordSCAU scau) // Verify screen status and file

	type public String VFMQ

	if %ProcessMode=2!(%ProcessMode=4)!(VFMQ="Q") do END quit

	do FILE(.scau)

	do END

	quit


FILE(RecordSCAU scau)	// File data

	type public Boolean ER
	type public String RELCIF(),UID
	
	type String RECIF
	
	if (%ProcessMode=0)!(%ProcessMode=1) do scau.save()

	if ER quit

	// Delete any existing SCAUR1 entries
	do Db.delete("SCAUR1","UID=:UID")
	
	set RECIF=""
	// Add SCAUR1 entries from the screen
	for  set RECIF=RELCIF(RECIF).order() quit:RECIF.isNull()  do {

		if RELCIF(RECIF).isNull() quit

		type RecordSCAUR1 scaur1=Class.new("RecordSCAUR1")
		set scaur1.uid=UID
		set scaur1.recif=RELCIF(RECIF)
		do scaur1.save()
		}

	quit


END	// End of processing

	type public String ER,RM,UID,VFMQ

	if ER!(%ProcessMode=2)!(%ProcessMode=4) quit

	if VFMQ="Q" do {
		// User ~p1 not created
		if %ProcessMode=0 set RM=$$^MSG(2873,UID)

		// User ~p1 not modified
		else  if %ProcessMode=1 set RM=$$^MSG(2875,UID)
		}
	else  do {
		// User ~p1 created
		if %ProcessMode=0 set RM=$$^MSG(2868,UID) quit

		// User ~p1 modified
		else  if %ProcessMode=1 set RM=$$^MSG(2872,UID) quit
		}

	set ER="W"

	quit


public RESET	// Reset user's manual revoke status

	type public String ER,RM

	type String %READ,%TAB(),UID,VFMQ

	set %TAB("UID")=".UID1/HLP=[SCAU]UID/TBL=[SCAU]"
	set %READ="@@%FN,,,UID/REQ" 

	do ^UTLREAD 

	if VFMQ="Q" quit
	
	type RecordSCAU scau=Db.getRecord("SCAU","UID=:UID")

	// Only save the SCAU record STATUS isn't already ACTIVE
	if $$STATUS^SCAUCDI(scau.%ucls,scau.lsgn,scau.mrstat,scau.pwdfail)'=1 do {

		set scau.pwdfail=0
		set scau.lsgn=%CurrentDate
		set scau.mrstat=0
		set scau.mreason=""

		do scau.save()
		}

	// User ~p1 not modified
	if ER set RM=$$^MSG(2875,UID) quit

	// User ~p1 modified
	else  set RM=$$^MSG(2872,UID)

	set ER="W"

	quit

 #OPTION ResultClass ON
public String vSIG()	quit "60604^26252^SSethy^4079"	// Signature - LTD^TIME^USER^SIZE
