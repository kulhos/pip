	#OPTION ResultClass ON
	
public String MRPC081(ret String RETURN,	// Return value (always null)
		      Number versn,		// Version number
		      String TABLE,		// DATA-QWIK table name
		      List elements)		// Elements to compile
	       
	/*
	Compile DQ Runtime Routines

	This procedure compiles DQ run-time programs for filers, procedures,
	data item protection and batch definitions.

	   RETURNS:
	      . $$ 	 Error message
	    		 Null = No error

	   EXAMPLES:

		set RM = $$^MRPC081(.val, 1, "DBTBL1", "CIF")
		set RM = $$^MRPC081(.val, 1, "DBTBL25", "BCHUTL")
		set RM = $$^MRPC081(.val, 1, "DBTBL33", "BCHCHKHLD7")

	---- Revision History ------------------------------------------------
	  
	01/21/2008 - RussellDS - CR30801
		Removed reference to DBTBL1.UDFILE.
		
		Removed #PACKAGE since it forces the use of the PSL v3.0
		compiler, which has problems with the $$V2LV^MSG call.
		
		Cleaned up code to avoid warnings.
		
		Removed old revision history.
	   
	05/01/07 - Mugilvannan - 26843
		Changed the argument variable name from table to TABLE
		to avoid "Invalid table name" error.
	----------------------------------------------------------------------
	*/
	
	type public Boolean ER
	type public String RM
	
	type Number i
	type String ET, name

	// Version number of client message is not compatible with server
	if (versn <> 1) quit $$ERRMSG^PBSUTL($$^MSG(2951))

	// Missing parameter
	if (TABLE.isNull() ! elements.isNull()) quit $$ERRMSG^PBSUTL($$^MSG(8607))

	set ER = false

	for i = 1:1:elements.count() do { quit:ER
		
		set name = elements.elementAt(i)
		
		// Protect variables
		type Number i
		type List elements
		type String pgm = ""

		do COMPILE(TABLE, name, .pgm)
		
		if 'ER, '((TABLE = "DBTBL1") & ((name = "DEP") ! (name = "LN"))) do RELINK(pgm)
	}

	if ER quit $$ERRMSG^PBSUTL(RM.get(), ET.get())

	set RETURN = $$V2LV^MSG("")

	quit ""


private void COMPILE(String TABLE,	// DATA-QWIK table name
		     String NAME,	// Element to compile
		     ret String pgm)	// Program compiled

	/*
	Compile the element indicated.
	
	If element is deposit or loan filer, because of the time they take to
	compile, spawn off the compilation process to a background job.
	*/
	
	type public Boolean ER
	type public String RM
	
	// Build filer
	if (TABLE = "DBTBL1") do {
		
		/* If the table is DEP or LN, then the compile process will
		   cause a timeout.  Therefore, we will spawn the compile
		   message out to a background process.
		   
		   NOTE:  This will have to be revisted for Java */
		   
		if ((NAME = "DEP") ! (NAME="LN")) do {

			type String PARM, PTMDIRID, X
			
			set PTMDIRID = CUVAR.PTMDIRID

			set PARM = "DEPLN^MRPC081("""_ NAME_ """)"

			set X = $$^%ZJOB(PARM, "PRO="_ PTMDIRID, 1)
		}
		else  do {
	
			do COMPILE^DBSFILB(NAME)
		
			if 'ER set pgm = "Record"_ NAME
			
		}
	}

	// Build Data Item Protection program
	else  if (TABLE = "DBTBL14") do {
		
		do BUILD^DBSPROT3(NAME)
		
		if 'ER do ^UPID(NAME, .pgm)
	}

	// Build procedure
	else  if (TABLE = "DBTBL25") do {
		
		do COMPILE^DBSPROC(NAME, .pgm)
	}

	// Build batch
	else  if (TABLE = "DBTBL33") do {
		
		type String cmpflg()

		do COMPILE^DBSBCH(NAME, cmpflg(), .pgm)
	}
	
	else  do {

		set ER = true
		// Invalid table name - ~p1
		set RM = $$^MSG(1484, TABLE)
	}

	quit
	
	
private void RELINK(String pgm)	// Routine to re-link to server

		
	/* Send control message to server to re-link program(s)
	   into server images.
			   
	   NOTE:  This will need to be revised for Java */

	if 'pgm.isNull() do CTRLMSG^PBSUTL("EXEC ZL """_ pgm_ """")

	quit
	

void DEPLN(String NAME)		// "DEP" or "LN" to compile

	/*
	Spawn label to compile the DEP and LN filers and then re-link them
	into the servers.
	*/
	
	type Boolean ER = false

	do COMPILE^DBSFILB(NAME)
	
	if 'ER do RELINK("Record"_ NAME)

	quit
 #OPTION ResultClass ON
public String vSIG()	quit "61237^74513^Dan Russell^3652"	// Signature - LTD^TIME^USER^SIZE
