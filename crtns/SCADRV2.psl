SCADRV2
 	/*
 	 PROCEDURE ID:	SCADRV2
 	     Original:	Dan S. Russell
		 Date:	9/30/2002 - 12:23:54 PM
 	         Desc:	PROFILE System Driver, Sub-routines for ^SCADRV0
 
	Provides various sub-routines for ^SCADRV0
	
	---- Revision History ------------------------------------------------
	12/08/05 - chhabris - CR16677
		   Modified SELECT section to included FUNMENU in the select
		   statement in order to display the function number if '??'
		   is selected in the menu prompt else the menu description
		   will be displayed.

	11/22/05 - chhabris - CR16677
		   Modified SELECT section to remove passing of global ^SCATBL
		   to DBSTBL and replaced the same by menuarr() which has got
		   submenu (function) number and description of the function.
		   The array is used for function display under the menu
		   selected. Removed variable P which was used to determine
		   piece of SCATBL(1 to display as we are passing an array.
		   Also cleaned up the code for variable scoping for SELECT
		   section.

	08/16/01 - ANTONOVV - 43583
		   Converterd to PSL

	---------------------------------------------------------------------
	*/
	quit


Public FIND(x)	// Find beginning with x

	new E67,X,TOP,BTM,ER
	set E67=20
	set TOP=MENU+5
	set X=$$^DBSTBL("[SCATBL]",x,"T","","",.TOP,.BTM,"[SCATBL3]")
	if $G(ER) do PNTRM^SCADRV0() set STATLIN=1 quit ""

	// Leave page up until next entry, unless too deep
	if TOP=(MENU+5) set STATLIN=1,%MSGS(TOP)="",%MSGS(BTM)=""
	else  set DISPLAY=1
	if X'="" set X="@"_X
	quit X
	

Public SELECT(x)  // Select - Display menu, menu w/functions, or all functions

	type public Number MENU,MENUNO,STATLIN
	type public String %MSGS(),DISPLAY
	type String menuarr()
	type Number E67,X,TOP,BTM,ER

	set E67=3
	if x.extract()="@" set x=x.extract(2,99)

	// Select on function
	if "??"'[x quit $$FIND(x.piece("?",1))
	
	set TOP=MENU+5

	type ResultSet rs=Db.select("SNUMB,FUNMENU,MDESC","SCAMENU","MNUMB=:MENUNO","SNUMB")
	while rs.next() do {
		if x="??" set menuarr(rs.getCol("SNUMB"))=rs.getCol("FUNMENU")
                else  set menuarr(rs.getCol("SNUMB"))=rs.getCol("MDESC")
		}

	set X=$$^DBSTBL("menuarr(","","N","","",.TOP,.BTM,,"")

	if ER.get() do PNTRM^SCADRV0() set STATLIN=1 quit ""

	// Leave page up until next entry, unless too deep
	if TOP=(MENU+5) set STATLIN=1,%MSGS(TOP)="",%MSGS(BTM)=""
	else  set DISPLAY=1

	quit X
	

Public KYB	// Emulate control keys

	new ZB
	set ZB=$$EMULATE^DBSMBAR
	if ZB="" set %fkey="ENT"
	else  set %fkey=%fkey(ZB)

	quit 
	

Public HELP(x)	// Display help, if available

	// MENU must be defined
	
	new FN,N,GEN,QUIT,HELP,TOP
	do HELPWHAT
	if FN="" do PNTRM^SCADRV0() quit 

	if FN'="@",'Db.isDefined("SCATBLDOC","FN,1") do NOHELP quit

	// DBSTBL.m requires global refernce if used with keys
	if '$D(HELP) set HELP="^SCATBL(3,"""_FN_""","

	// Display
	set TOP=MENU+5
	do ^DBSHLP(HELP,.TOP)

	// Clear page, redisplay only if necessary
	if TOP=(MENU+5) write $$CUP^%TRMVT(1,TOP),$$CLP^%TRMVT set STATLIN=1
	else  set DISPLAY=1

	quit 
	

NOHELP	// Handle no help available message

	// Help documentation for ~p1 is not available
	set RM=$$^MSG("1175",FN)
	do PNTRM^SCADRV0()
	quit 
		

HELPWHAT	// Find the function or menu for which to print help

	// Return FN as name and HELP as table
	if "@"[x set FN="SCADRV" quit			// Driver help
	if $E(x)="@" do HELPAT quit 

	type RecordSCAMENU scamenu=Db.getRecord("SCAMENU","MENUNO,x",1)
	if scamenu.getMode() set FN=scamenu.funmenu quit

	set N=$$STRMTCH^SCADRV0(MENUNO,x)

	//Invalid function name
	if N="" set FN="",RM=$$^MSG(1359) quit	

	type RecordSCAMENU scamenu=Db.getRecord("SCAMENU","MENUNO,N",1)
	if scamenu.getMode() set FN=scamenu.funmenu

	quit 
		

HELPAT	// Handle @xxx

	set FN=$E(x,2,99)

	// Invalid menu number
	if FN?.N,('Db.isDefined("SCAMENU0","FN")) set FN="",RM=$$^MSG(1403)

	// Invalid function name
	if 'FN?.N,('Db.isDefined("SCATBL","FN")) set FN="",RM=$$^MSG(1359)

	quit 
	

DUMMY	// Private - Tied to function SCADRV
	/*
	This call simply quits.  Provides valid call point for function
	SCADRV to allow the function to exist to support function help
	documentation.
	*/
	quit 
 #OPTION ResultClass ON
public String vSIG()	quit "60243^25879^Sanjay Chhrabria^3980"	// Signature - LTD^TIME^USER^SIZE
