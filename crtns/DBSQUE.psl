DBSQUE	/* Public - Return Report Writer Queries for queuing system

	ORIG: CHIANG - 24 SEP 1993
	PROCEDURE ID: DBSRWQRY
	DESC: DATA-QWIK Report Query Parser


	---- Comments ----------------------------------------------------------
	LIBRARY:

		. REP(RID,PROMPT)	- Access by report name
		. FUN(FN,PROMPT)	- Access by function name

	------ Revision History ------------------------------------------------
	
	08/28/05 - RussellDS - CR17021
		   Fix problem with PQINFO array in REP section when node 1
		   may not be defined.

        07/26/05 - SkariahV- CR16679
	           Removed #WARN directive.
	              
	01/13/03 - Dan Russell - 51351
		   Moved into PSL.

	------------------------------------------------------------------------

	I18N=QUIT Excluded from I18N standards

	*/


	quit					// Can't call from top


REP(String RID,		// Public - Return prompts for specified report
	Number OPT,
	String PROMPTS())

	/*
	ARGUMENTS:

		. RID		Report Name		/REQ/NONULL/MECH=VAL

		. OPT		Obsolete		/NOREQ/DEF=0/MECH=VAL
				Was option for either queuing system
				or DQRT.  DQRT no longer exists.  Retained
				for compatibility, but not used.

		. PROMPTS	Return query prompts	/MECH=REFNAM:W

				prompt(SEQ)=PROMPT

	EXAMPLE:
		D REP^DBSQUE("SCA123",0,.ABC)

			ABC(1)="* Device"
			ABC(2)="* Run Date"		<--- * is required indicator
			ABC(3)="Form Sequence"
			ABC(4)="Institution Number"
	*/

	type Public Number ER=0
	type Public String RM

	type Number CNT,SEQ
	type String PQINFO()

	type RecordDBTBL5H dbtbl5h=Db.getRecord("DBTBL5H","LIBS=""SYSDEV"",RID=:RID",1)

	quit:'dbtbl5h.getMode()				// Report does not exist

	set RM=$$^DBSRWQRY(.dbtbl5h,.PQINFO)		// Get query info
	if RM'="" set ER=1 quit				// Query syntax error

	// * Device
	set PROMPTS(1)=$$^MSG(8047)			// Device prompt

	set SEQ=""
	for CNT=2:1 set SEQ=$O(PQINFO(SEQ)) quit:SEQ=""  do {
		
		type String PROMPT
		
		if PQINFO(SEQ,1).exists() do {

			set PROMPT=PQINFO(SEQ,1).piece("|",10)
			// Required
			if PQINFO(SEQ,1).piece("|",11) set PROMPT="* "_PROMPT
			set PROMPTS(CNT)=PROMPT
		}
	}

	quit



FUN(String FN,		 // Public - Return prompts for specified function
	Number OPT,
	String PROMPTS())

	/*
	ARGUMENTS:

		. FN		Function Name		/REQ/NONULL/MECH=VAL

		. OPT		Obsolete		/NOREQ/DEF=0/MECH=VAL
				Was option for either queuing system
				or DQRT.  DQRT no longer exists.  Retained
				for compatibility, but not used.

		. PROMPTS	Return query prompts	/MECH=REFNAM:W

				prompt(SEQ)=PROMPT

	EXAMPLE:
		D FUN^DBSQUE("REP123",0,.ABC)

			ABC(1)="* Device"
			ABC(2)="* Run Date"		<--- * is required indicator
			ABC(3)="Form Sequence"
			ABC(4)="Institution Number"
	*/

	type String RID

	type RecordSCATBL scatbl=Db.getRecord("SCATBL","FN=:FN",1)

	quit:'scatbl.getMode()				// Invalid function
	quit:scatbl.pgm'["^URID"			// Not a report function
	quit:scatbl.prp'["RID="				// Invalid pre-processor

	#ACCEPT DATE=01/13/03;PGM=Dan Russell;CR=Mark Spier
	xecute scatbl.prp				// Get report name

	do REP(RID,0,.PROMPTS)
	quit


QA	// Private - Test all reports

	type String IO

	do ^SCAIO

	use IO

	type ResultSet rs=Db.select("RID","DBTBL5H","LIBS='SYSDEV'","RID ASC")

	while rs.next() do {
		type Number N
		type String PROMPTS(),RID

		set RID=rs.getCol("RID")
		do REP(RID,0,.PROMPTS)
		write !,RID,!
		set N=""
		for  set N=$O(PROMPTS(N)) quit:N=""  write ?20,PROMPTS(N),!
		write !
		}

	close IO

	quit
 #OPTION ResultClass ON
public String vSIG()	quit "60148^65610^Dan Russell^3257"	// Signature - LTD^TIME^USER^SIZE
